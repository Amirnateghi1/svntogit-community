# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sylpheed
pkgver=3.2.0
pkgrel=1
pkgdesc="Lightweight and user-friendly e-mail client"
arch=('i686' 'x86_64')
url="http://sylpheed.sraoss.jp/en/"
license=('GPL')
depends=('gpgme' 'gtkspell' 'desktop-file-utils')
makedepends=('compface' 'openssl')
options=('!libtool')
install="$pkgname.install"
source=("http://sylpheed.sraoss.jp/$pkgname/v3.2/$pkgname-$pkgver.tar.bz2")
md5sums=('bbb691cf2bb11f84b329e68b58a62918')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-ldap
  make

  # Build Attachment-Tool Plug-in
  cd plugin/attachment_tool && make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Install Attachment-Tool Plug-in
  cd plugin/attachment_tool
  make DESTDIR="$pkgdir/" install-plugin
}
