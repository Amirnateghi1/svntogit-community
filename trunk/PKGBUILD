# Maintainer: Alad Wenter <alad@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=asoundconf
pkgver=1.2
_revision=bf25f416f615a43267abecab4c599e86f363438b # 1.2
pkgrel=2
pkgdesc="utility to read and change the user's ALSA library configuration"
arch=('any')
url="https://bitbucket.org/stativ/asoundconf"
license=('GPL')
depends=('python2')
makedepends=('mercurial')
optdepends=('pygtk: asoundconf-gtk GUI')
source=("hg+https://bitbucket.org/stativ/asoundconf#revision=$_revision")
sha256sums=('SKIP')

build() {
    cd "$pkgname"
    python2 setup.py build
}

package() {
    cd "$pkgname"
    python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
