# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=libwebsockets
pkgver=2.4.0
pkgrel=1
pkgdesc="C library for websocket clients and servers"
arch=('x86_64')
url="https://libwebsockets.org"
depends=('openssl')
makedepends=('cmake')
license=('LGPL')
source=("https://github.com/warmcat/libwebsockets/archive/v$pkgver.tar.gz")
md5sums=('2bcd5ceccca6c057629c46ab380cf2ac')

build() {
  cd "$pkgname-$pkgver"

  mkdir build && cd build
  # http2 support -DLWS_WITH_HTTP2=1
  cmake -D CMAKE_BUILD_TYPE=RelWithDebInfo -DCMAKE_INSTALL_PREFIX:PATH=/usr ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="${pkgdir}" install

  cd ..

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
