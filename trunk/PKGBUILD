# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=liboping
pkgver=1.6.0
pkgrel=2
pkgdesc='C library to generate ICMP echo requests, better known as "ping packets"'
url='http://verplant.org/liboping/'
arch=('i686' 'x86_64')
license=('GPL')
options=('!libtool')
depends=('libcap')
makedepends=('ncurses')
optdepends=('perl: perl bindings'
            'ncurses: noping CLI tool')
source=("${url}files/$pkgname-$pkgver.tar.gz"
        'newwin-explicit-width.patch')

sha1sums=('18f8795bd32cbd795eac9ca06813b5484849089c'
          'c221fa06fbfde1da8cce6a2d1adf820fc0e3feb7')

install=install

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i ../newwin-explicit-width.patch
	./configure --prefix=/usr --enable-static=no
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}
