# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=libmemcached
pkgver=1.0.11
pkgrel=1
pkgdesc="C and C++ client library to the memcached server"
arch=('i686' 'x86_64')
url="http://libmemcached.org/"
license=('GPL')
depends=('glibc' 'libsasl' 'libevent')
makedepends=('perl')
options=('!libtool')
source=(http://launchpad.net/$pkgname/1.0/$pkgver/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('d1bf75110fa9220c0c2287e6fc81ce70b83d91453ce09120553f8f531ffd2137')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # memcached is needed only in `make test', therefore we pass
  # --without-memcached to the configure script to skip it
  ./configure --prefix=/usr --without-memcached
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
