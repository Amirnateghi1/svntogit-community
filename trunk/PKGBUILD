# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Dave Pretty <david dot pretty at gmail dot com>

_core_i18n=88621bc
_desktop_ftl=2c96d2d
_desktop_i18n=edcf8c8

pkgname=anki
pkgver=2.1.25
pkgrel=1

pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently"
url="https://ankisrs.net/"
arch=('x86_64')
license=('AGPL3')

depends=(
	# anki and aqt
	'python-beautifulsoup4'
	'python-requests'
	'python-wheel'

	# anki
	'python-decorator'
	'python-distro'
	# Currently not packaged.  Falls back to stdlib json module.
	# 'python-orjson'
	'python-protobuf'

	# aqt
	'python-jsonschema'
	'python-markdown'
	'python-pyaudio'
	'python-pyqt5'
	'python-pyqtwebengine'
	'python-send2trash'
)
makedepends=(
	'git'
	'rsync'

	'maturin'
	'rustup'

	'python-pip'
	'python-mypy-protobuf'
	'npm'
	'typescript'
)
optdepends=(
	'lame: record sound'
	'mpv: play sound. prefered over mplayer'
	'mplayer: play sound'
)

source=(
	anki-$pkgver.tar.gz::https://github.com/ankitects/anki/archive/$pkgver.tar.gz

	ankitects-anki-core-i18n-$_core_i18n.tar.gz::https://github.com/ankitects/anki-core-i18n/tarball/$_core_i18n
	ankitects-anki-desktop-ftl-$_desktop_ftl.tar.gz::https://github.com/ankitects/anki-desktop-ftl/tarball/$_desktop_ftl
	ankitects-anki-desktop-i18n-$_desktop_i18n.tar.gz::https://github.com/ankitects/anki-desktop-i18n/tarball/$_desktop_i18n

	0001-Move-aqt_data-to-sys.prefix-share.patch
	0002-Remove-bad-build-steps-from-makefiles.patch
	0003-Compile-.py-s-before-building-wheel.patch
	0004-Disable-auto-updates.patch
)
sha256sums=('b19c91c957055d25a91abebd6c61f53d6e23ac972d39639c54465918440fa853'
            '221929f097436f5b4c5a47d58ba5bbde8b421477af61872e4374a509b559cc02'
            '358d06085424a2bd71865cea79d4ca7d2c82d9abcdb7382f11ab07cb41592320'
            '7cbec154a2506c39550ba7c5ef7780ee42b3480296b1e480912c78cca20f8434'
            '26162e6370e7763a8ec37e1690dc39c8b0f71e9d2524c452c330276b357cb18f'
            '1bf87c408fd9e2a09cb39d8353af1e0dc64930ac819b4ffae555b14ed4644981'
            '401d31ac5992c342778ba748eac6179da9d594e1ef93d0db8170fad643bab069'
            '97048aa118281311f17b1e2154f55a2a2f1d84cbebbdc93459bca49e8f8f06fd')

prepare() {
	cd anki-$pkgver
	patch -p1 <"$srcdir"/0001-Move-aqt_data-to-sys.prefix-share.patch
	patch -p1 <"$srcdir"/0002-Remove-bad-build-steps-from-makefiles.patch
	patch -p1 <"$srcdir"/0003-Compile-.py-s-before-building-wheel.patch
	patch -p1 <"$srcdir"/0004-Disable-auto-updates.patch

	# Put translations in place.
	ln -sf "$srcdir"/ankitects-anki-core-i18n-*/ rslib/ftl/repo
	ln -sf "$srcdir"/ankitects-anki-desktop-ftl-*/ qt/ftl/repo
	ln -sf "$srcdir"/ankitects-anki-desktop-i18n-*/ qt/po/repo
}

build() {
	# Built into the shared libraries so that the Python component can check
	# that it has the same value.
	cd anki-$pkgver/meta
	echo arch-linux-$pkgver-$pkgrel > buildhash

	# rust ankirspy module
	cd ../rspy
	make build

	# python anki module
	cd ../pylib
	make build

	# python aqt module
	cd ../qt
	make build
}

package() {
	cd anki-$pkgver

	PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps dist/*.whl

	install -Dm755 qt/runanki "$pkgdir"/usr/bin/anki
	install -Dm644 qt/anki.desktop "$pkgdir"/usr/share/applications/anki.desktop
}
