# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=firefox-stylus
pkgver=1.5.14
pkgrel=1
pkgdesc='Easily install themes and skins for many popular sites'
url=https://add0n.com/stylus.html
arch=('any')
license=('GPL3')
groups=('firefox-addons')
makedepends=('git' 'nodejs' 'npm')
source=("https://github.com/openstyles/stylus/archive/v$pkgver/stylus-v$pkgver.tar.gz")
b2sums=('8e3591ccea567d4cdc74262b89ddedcfadac103f5b457edb4b0ddcbc8c5f602206732960194ed78aa5dc04157c8c285b2fd7a0969b2b53ae14ac65d09423a080')

build() {
  cd stylus-$pkgver
  npm install
  npm audit fix
  npm run zip
}

package() {
  cd stylus-$pkgver
  install -Dm644 stylus.zip "$pkgdir"/usr/lib/firefox/browser/extensions/{7a7a4a92-a2a0-41d1-9fd7-1e92480d612d}.xpi
}

# vim:set ts=2 sw=2 et:
