# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=19.03
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('63c25047cc35d05809f3296c7eba41522ccc0dc98f3d2410b32fa3b5e8eed63ff7f3c3593b1d8c5ac2a81c58f56ff84d050f4c3ac2f4197309be4be1aba59da1'
            '9a2a2addb671a46589a973cda6de2cc3cc6918b9ece44a9a9d2ded5fb4031909e5993720199e156a6c2bbde1c1a409bc1886ac1d2fd896a730ff16f029c32b64'
            'b05bd2e83cd11e19dfdc6efe7fa818cf65f6db6f316996b6b2cdc48a62986d94141924e8495840a12be46dced59426be1131b203ec976e0d918f109c98d572ca'
            '7dd418779b8e15cfb787612a12c915380d6263c77a3b65cb729f1d3d3dfe961b9051185b27ffcad938108c39fc7db7a7a048ef1329e383a84ecb69a3d347e6ed')
