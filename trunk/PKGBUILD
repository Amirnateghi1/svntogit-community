# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Julien Duponchelle <julien@gns3.net>

pkgname=python-cx_freeze
pkgver=6.1
pkgrel=1
pkgdesc='Utilities for freezing Python scripts into executables'
arch=('x86_64')
url='https://anthony-tuininga.github.io/cx_Freeze'
license=('PSF')
depends=('python')
conflicts=('cx_freeze')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-openpyxl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/anthony-tuininga/cx_Freeze/archive/$pkgver.tar.gz")
sha512sums=('571194251f4a7ce01df6bc324b54d9c74c605446aa78dd175b0fb804663d94f2edbbc31d2eaa96a295f148909f4383119dd38d2dfdf8554158137d45b3c86c05')

build() {
  cd cx_Freeze-$pkgver
  python setup.py build
}

check() {
  cd cx_Freeze-$pkgver
  python setup.py nosetests || warning "Tests failed"
}

package() {
  cd cx_Freeze-$pkgver
  python setup.py install --root "$pkgdir" --optimize 1 --skip-build
}
