# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=libwebsockets
pkgver=4.0.19
pkgrel=1
pkgdesc="C library for websocket clients and servers"
arch=('x86_64')
url="https://libwebsockets.org"
depends=('glibc' 'openssl' 'libuv' 'libev' 'zlib')
makedepends=('cmake' 'libcap')
provides=('libwebsockets.so')
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://github.com/warmcat/libwebsockets/archive/v$pkgver.tar.gz)
sha256sums=('3bdf0fbf5c396f39d1bc48b7360d598a50209c2d143d03467d4f42cb8bf91c5f')

build() {
  cd "$pkgname-$pkgver"
  cmake -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_BUILD_TYPE='None' \
        -D LWS_WITH_HTTP2=ON \
        -D LWS_IPV6=ON \
        -D LWS_WITH_ZIP_FOPS=ON \
        -D LWS_WITH_SOCKS5=ON \
        -D LWS_WITH_RANGES=ON \
        -D LWS_WITH_ACME=ON \
        -D LWS_WITH_LIBUV=ON \
        -D LWS_WITH_LIBEV=ON \
        -D LWS_WITH_LIBEVENT=OFF \
        -D LWS_WITH_FTS=ON \
        -D LWS_WITH_THREADPOOL=ON \
        -D LWS_UNIX_SOCK=ON \
        -D LWS_WITH_HTTP_PROXY=ON \
        -D LWS_WITH_DISKCACHE=ON \
        -D LWS_WITH_LWSAC=ON \
        -D LWS_LINK_TESTAPPS_DYNAMIC=ON \
        -D LWS_WITHOUT_BUILTIN_GETIFADDRS=ON \
        -D LWS_USE_BUNDLED_ZLIB=OFF \
        -D LWS_WITHOUT_BUILTIN_SHA1=ON \
        -D LWS_WITH_STATIC=OFF \
        -D LWS_WITHOUT_CLIENT=OFF \
        -D LWS_WITHOUT_SERVER=OFF \
        -D LWS_WITHOUT_TESTAPPS=ON \
        -D LWS_WITHOUT_TEST_SERVER=OFF \
        -D LWS_WITHOUT_TEST_SERVER_EXTPOLL=ON \
        -D LWS_WITHOUT_TEST_PING=ON \
        -D LWS_WITHOUT_TEST_CLIENT=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libcap.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
