# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeff Mickey <j@codemac.net>
# Contributor: Dusty Phillips <dusty@buchuki.com>
pkgname=jedit
pkgver=4.4.2
pkgrel=2
pkgdesc="Java based extensible text editor"
arch=('any')
url="http://www.jedit.org/"
license=('GPL')
depends=('sh' 'java-environment' 'desktop-file-utils')
install=jedit.install
noextract=("${pkgname}${pkgver}install.jar")
source=("http://downloads.sourceforge.net/$pkgname/${pkgname}${pkgver}install.jar"
        'jedit.sh'
        'jeditbg.sh'
        'jedit.props'
        'jedit.desktop')
md5sums=('54d05ed3adc68a02066ed41848dfeaad'
         '1be20f12332e4f769dc454aae94a9101'
         '53a805da6f1252480a2fa6db6ab7693d'
         '94eaf0d29f1af8c813e8bd580c34a289'
         '1440d194edc3c65a065ca978887a0c17')

build() {
  cd "$srcdir"

  msg2 "Extracting..."
  java -jar ${pkgname}${pkgver}install.jar auto \
    "$pkgdir/usr/share/java/$pkgname/" \
    unix-script="$pkgdir/usr/bin/" \
    unix-man="$pkgdir/usr/share/man/man1" > inst.log
}

package() {
  cd "$srcdir"

  install -Dm755 "jedit.sh" "$pkgdir/usr/bin/jedit"
  install -Dm755 "jeditbg.sh" "$pkgdir/usr/bin/jeditbg"
  install -Dm644 "jedit.props" "$pkgdir/usr/share/jedit/default.props"
  install -Dm644 "jedit.desktop" "$pkgdir/usr/share/applications/jedit.desktop"
  install -Dm644 "$pkgdir/usr/share/java/jedit/doc/jedit.png" \
    "$pkgdir/usr/share/pixmaps/jedit.png"
}

# vim:set ts=2 sw=2 et:
