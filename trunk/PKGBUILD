# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>

pkgbase=python-selenium
pkgname=('python-selenium' 'python2-selenium')
pkgver=3.5.0
pkgrel=1
pkgdesc="Python binding for Selenium Remote Control"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/selenium"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('geckodriver: Firefox driver support')
source=("https://pypi.python.org/packages/ac/d7/1928416439d066c60f26c87a8d1b78a8edd64c7d05a0aa917fa97a8ee02d/selenium-3.5.0.tar.gz")
md5sums=('986702fdd0e2aec6a4eda134678b8b3f')

package_python-selenium() {
  depends=('python')
  cd "$srcdir/selenium-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-selenium() {
  depends=('python2')
  cd "$srcdir/selenium-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# check() takes too many dependencies to run
