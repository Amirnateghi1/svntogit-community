# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>   

pkgname=mldonkey
pkgver=3.1.3
pkgrel=1
pkgdesc='Multi-network P2P client'
arch=('i686' 'x86_64')
url="http://mldonkey.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'file' 'gd' 'bzip2')
makedepends=('lablgtk2' 'librsvg' 'ocaml')
optdepends=('librsvg: GUI support'
            'libx11: GUI support')
backup=('etc/conf.d/mldonkey')
install=mldonkey.install
source=(http://downloads.sourceforge.net/sourceforge/mldonkey/$pkgname-$pkgver.tar.bz2
        mldonkeyd
        mldonkey.conf)
md5sums=('671f60467a918a9b7c2affef63ff5c25'
         '9e90003a012cf17b548dcc647961ffa6'
         '1f86d92db51c62cded0e6f3833057dee')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-gui=newgui2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/$pkgname-$pkgver/icons/rsvg/type_source_normal.svg" \
    "$pkgdir/usr/share/icons/mldonkey.svg"

  install -Dm755 "$srcdir/mldonkeyd" "$pkgdir/etc/rc.d/mldonkey"
  install -Dm644 "$srcdir/mldonkey.conf" "$pkgdir/etc/conf.d/mldonkey"
  install -Dm644 "distrib/mldonkey.desktop" \
    "$pkgdir/usr/share/applications/mldonkey.desktop"
}
