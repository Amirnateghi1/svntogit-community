# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: poisonby <poisonby@tutanota.com>
# Contributor: Jason Scurtu (scujas) <jscurtu@gmail.com>
# Contributor: Marcin Tydelski <marcin.tydelski@gmail.com>
# Contributor: Jan Lukas Gernert (JeanLuc) <https://launchpad.net/~eviltwin1>

pkgname=feedreader
_pkgname=FeedReader
pkgver=2.0.2+559+g78d7ced8
pkgrel=1
pkgdesc="Modern desktop application designed to complement existing web-based RSS accounts"
arch=(x86_64)
url="https://jangernert.github.io/FeedReader/"
license=(GPL3)
depends=(curl gnome-online-accounts libgee libpeas)
makedepends=(cmake git gobject-introspection vala)
_commit=78d7ced862aa491d64d9b5f135b2ba2d893b79d1  # master
source=("git+https://github.com/jangernert/$_pkgname#commit=$_commit"
        "git+https://github.com/fivefilters/ftr-site-config")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname
  git config submodule.data/GrabberConfig.url "$srcdir/ftr-site-config"
  git submodule update --init
  mkdir build
}

build() {
  cd $_pkgname/build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DGSETTINGS_COMPILE=OFF
  make -j1
}

package() {
  cd $_pkgname/build
  make DESTDIR="$pkgdir" install
}
