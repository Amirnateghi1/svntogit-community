# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Dave Pretty <david dot pretty at gmail dot com>

pkgname=anki
pkgver=2.1.2
pkgrel=1

pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently"
url="https://ankisrs.net/"
arch=('x86_64')
license=('AGPL3')

depends=('qt5-webengine' 'python-beautifulsoup4' 'python-pyaudio'
         'python-pyqt5' 'python-requests' 'python-send2trash'
         'python-decorator' 'python-markdown'
         'desktop-file-utils')
optdepends=('lame: record sound'
            'mplayer: play sound')

source=(https://apps.ankiweb.net/downloads/current/anki-$pkgver-linux-amd64.tar.bz2
        makefile-fix.patch)
sha256sums=('e020d1b01c2075573f118fd6a0fa86d468a93ef43f3b72e9c6a9484bfd140b03'
            '4dd1c4c93fc50cfd6d0c52c4aae24bea23554840a3fd1b46dcb47305ee618668')

prepare() {
  cd anki-$pkgver-linux-amd64

  patch -p1 <"$srcdir"/makefile-fix.patch
}

package() {
  cd anki-$pkgver-linux-amd64

  make DESTDIR="$pkgdir" PREFIX=/usr install
}
