# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=17.03
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

md5sums=('58f6b53b06bcab6d90ecccd1bce6ad8c'
         '065db44c6603b1cdd7a51d668418d034'
         '8fc2ff794662d288093b75b46cfc0fda'
         '3a8223bbaa51eb7be6b0d194aba1f276'
         '56820338824d77cc65cb35a36bfef0d4'
         '23353b5706b305cf4951519cf5479ff7'
         'd73250e0e758d4fdf62a7fb2bcad8ffa'
         '5d731f14ee0c5108bcfde71c95bd2956'
         'fe62aaf3e024d6cd56f4ef352e16a0b5'
         'd8de8d5627676d4fa3df1a28075b80aa')
