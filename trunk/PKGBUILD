# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
pkgname=tpm2-tss
pkgver=3.0.2
pkgrel=2
pkgdesc='Implementation of the TCG Trusted Platform Module 2.0 Software Stack (TSS2)'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-tss'
license=('BSD')
depends=('curl' 'json-c' 'openssl' 'libjson-c.so')
makedepends=('cmocka' 'doxygen')
checkdepends=('iproute2' 'swtpm' 'uthash')
provides=('libtss2-esys.so' 'libtss2-fapi.so' 'libtss2-mu.so' 'libtss2-rc.so' 'libtss2-sys.so' 'libtss2-tctildr.so')
backup=('etc/tpm2-tss/fapi-config.json'
        'etc/tpm2-tss/fapi-profiles/P_ECCP256SHA256.json'
        'etc/tpm2-tss/fapi-profiles/P_RSA2048SHA256.json')
options=('!emptydirs')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        'tpm2-tss-3.0.2_fix_rel_path_to_abs_path.patch::https://patch-diff.githubusercontent.com/raw/tpm2-software/tpm2-tss/pull/1914.patch')
sha512sums=('0b295bb1a135c904344e8a11ae9a293f71d60ea045bacd59a9b778ba19e88e1b91022f24a4775615c215db450584574901838146bc4d4d286ee8b686b72b0bab'
            'SKIP'
            '742c3ee9350c76ef0c38e2beec95ca48d44ad49b8ec14e8efd065b4e76a52b77ebcfc9855d12b7ef03c31857867ca65c40cd76032cab9e8f8cd91cd9b234d1ca')
validpgpkeys=('D760B790CCF0A41CBE7B047C316CC1FB24ABDC72'  # Tadeusz Struk <tadeusz.struk@intel.com>
              'D6B4D8BAC7E0CC97DCD4AC7272E88B53F7A95D84'  # Andreas Fuchs <andreas.fuchs@sit.fraunhofer.de>
              '5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1') # William Roberts (Bill Roberts) <william.c.roberts@intel.com>

prepare() {
	cd "$pkgname-$pkgver"
	# Fix a bug that leads to tpm2-pkcs11 test failures (https://github.com/tpm2-software/tpm2-tss/pull/1914)
	patch --forward --strip=1 --input="$srcdir/tpm2-tss-3.0.2_fix_rel_path_to_abs_path.patch"
}

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	            --with-runstatedir=/run --with-sysusersdir=/usr/lib/sysusers.d \
	            --with-tmpfilesdir=/usr/lib/tmpfiles.d --with-udevrulesprefix=60- \
	            --enable-unit $( ((CHECKFUNC)) && echo --enable-integration)
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
