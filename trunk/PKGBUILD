# Maintainer: David Runge <dave@sleepmap.de>

pkgname=redkite
pkgver=0.6
pkgrel=2
pkgdesc="A small free software and cross-platform GUI toolkit"
arch=('x86_64')
url="https://github.com/quamplex/redkite"
license=('GPL3')
depends=('cairo')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/quamplex/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('2b74163ac3e66cdba39daee81c646676b9d32a57821dd851a077f04eafa5e076c2fa2e54b46cb0e1d6506d840c527868e15d863dec64271ee436f497185853c2')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -pv build
}

build() {
  cd "$pkgname-$pkgver"
  (
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          ..
    make VERBOSE=1
  )
}

package() {
  cd "$pkgname-$pkgver"
  (
    cd build
    make DESTDIR="$pkgdir/" install
  )
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
