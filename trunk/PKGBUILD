# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Gabriel Souza Franco <Z2FicmllbGZyYW5jb3NvdXphQGdtYWlsLmNvbQ==>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Giuseppe Borzi <gborzi@ieee.org>
# Contributor: Brice Méalier <mealier_brice@yahoo.fr>
# Contributor: Michele Mocciola <mickele>

pkgname=opencascade74
pkgver=7.4.0
pkgrel=1
pkgdesc='Open CASCADE Technology, 3D modeling & numerical simulation'
arch=(x86_64)
url='https://www.opencascade.org'
license=(custom)
depends=(ffmpeg freeimage gl2ps intel-tbb tk vtk)
makedepends=(cmake git ninja qt5-base)
conflicts=(opencascade)
source=("$pkgname.tgz::https://git.dev.opencascade.org/gitweb/?p=occt.git;a=snapshot;h=fd47711d682be943f0e0a13d1fb54911b0499c31;sf=tgz"
        opencascade.sh
        fix-install-dir-references.patch
        cmake-fix-variable.patch)
sha256sums=('af73c89ade0467ca7351dc445e4bd20832387da1e205f6955bc9356ab7d9f22b'
            '2064536a85d46fee368a8f1a712b2c6c77ca79c5bffcc68cba79d70d36efa2f4'
            'afb584aa453993ae8d9e2b983594558531ede735a5892754b812be30650c9fb5'
            'c7427082400b2d1ac58c59549d4dd1ee5b27d06847b7907842e9e2f2b68980fa')

prepare() {
  cd occt-fd47711
  patch -Np1 -i ../fix-install-dir-references.patch
  patch -Np1 -i ../cmake-fix-variable.patch
}

build() {
  mkdir -p build
  cd build
  cmake ../occt-fd47711 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_FFMPEG=ON \
    -DUSE_FREEIMAGE=ON \
    -DUSE_GL2PS=ON \
    -DUSE_TBB=ON \
    -DUSE_VTK=ON \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm755 opencascade.sh "$pkgdir/etc/profile.d/opencascade.sh"
  install -d "$pkgdir/usr/share/licenses/$pkgname/"
  mv "$pkgdir/usr/share/doc/opencascade/"* "$pkgdir/usr/share/licenses/$pkgname"
  rm -r "$pkgdir/usr/share/doc" "$pkgdir/usr/bin/"*.sh
}
