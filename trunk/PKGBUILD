# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>

pkgname=espeak
pkgver=1.46.02
pkgrel=2
pkgdesc="Text to Speech engine for good quality English, with support for other languages"
arch=('x86_64' 'i686')
url='http://espeak.sourceforge.net/'
license=('GPL')
depends=('portaudio' 'libpulse')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-source.zip")
sha256sums=('ef8beba25bd261718582d5cbb1f10293007eb525b705ff79c9fb3f505dd954ba')

build() {
  cd "$srcdir/$pkgname-$pkgver-source/src"

  cp portaudio19.h portaudio.h
  make CXXFLAGS="$CXXFLAGS" AUDIO=runtime
}

package() {
  cd "$srcdir/$pkgname-$pkgver-source/src"

  make DESTDIR=$pkgdir install
  chmod 644 "$pkgdir/usr/lib/libespeak.a"
}

# vim:set ts=2 sw=2 et:
