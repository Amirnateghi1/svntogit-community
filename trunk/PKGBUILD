# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: niQo
# Contributor: Christoph Siegenthaler <csi@gmx.ch>

pkgname=espeak
pkgver=1.45.04
pkgrel=1
pkgdesc="Text to Speech engine for good quality English, with support for other languages"
arch=('i686' 'x86_64')
url="http://espeak.sourceforge.net/"
license=('GPL')
depends=('gcc-libs' 'portaudio')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-source.zip)
md5sums=('6e810d2786b55cddb34f31b3eb813507')

build() {
	cd $srcdir/$pkgname-$pkgver-source/src
	cp portaudio19.h portaudio.h
	make
}
package() {
	cd $srcdir/$pkgname-$pkgver-source/src
	make DESTDIR=$pkgdir install
	find "$pkgdir/usr/share/espeak-data" -type f -print0 |xargs -0 chmod 644
}
