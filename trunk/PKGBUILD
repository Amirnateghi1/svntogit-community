# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>

pkgname=tpm2-tss
pkgver=2.3.3
pkgrel=1
pkgdesc="Implementation of the TCG Trusted Platform Module 2.0 Software Stack (TSS2)"
arch=(x86_64)
url="https://github.com/tpm2-software/tpm2-tss"
license=(BSD)
depends=(openssl)
makedepends=(doxygen)
checkdepends=(cmocka ibm-sw-tpm2 iproute2 procps-ng uthash)
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "tpm2-tss-2.3.2_ibm-sw-tpm2-1563.patch::${url}/commit/6ff9ad43cafa6aeebd264c78ea56e97b2a98e14b.patch")
sha512sums=('df83908508fc8add8ca1aaf7144bcf369f13797f7e5d39fe29654d82e2de2cddf324cd06cb3154318ef767d706d863a7781d98c7b29a90945f081a54594128e5'
            'SKIP'
            '62608e0bf811cb62b75b8fd458118f581b509529630fcb9eef3fca1c1d3262e3b7d48e334ebda28e712af406b8dbb23a2b87a2b4d0b1883a23d1d497b45bd86e')
validpgpkeys=(D760B790CCF0A41CBE7B047C316CC1FB24ABDC72  # Tadeusz Struk <tadeusz.struk@intel.com>
              D6B4D8BAC7E0CC97DCD4AC7272E88B53F7A95D84  # Andreas Fuchs <andreas.fuchs@sit.fraunhofer.de>
              5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1) # William Roberts (Bill Roberts) <william.c.roberts@intel.com>

prepare() {
	cd ${pkgname}-${pkgver}
	# Fix test failure with ibm-sw-tpm2 1563, see https://github.com/tpm2-software/tpm2-tss/pull/1585
	patch --strip=1 --input="${srcdir}/tpm2-tss-2.3.2_ibm-sw-tpm2-1563.patch"
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr --with-udevrulesprefix=60- $( ((CHECKFUNC)) && echo --enable-unit --enable-integration)
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
    echo 'u tss - "tss user for tpm2"' | install -Dm644 /dev/stdin "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    rm "${pkgdir}"/usr/lib/libtss2-tcti-default.so # https://github.com/tpm2-software/tpm2-tss/issues/1482#issuecomment-516363966
}
