# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Julien Duponchelle <julien@gns3.net>

pkgname=python-cx_freeze
pkgver=6.0
pkgrel=1
pkgdesc='Utilities for freezing Python scripts into executables'
arch=('x86_64')
url='https://anthony-tuininga.github.io/cx_Freeze'
license=('PSF')
depends=('python')
conflicts=('cx_freeze')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-openpyxl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/anthony-tuininga/cx_Freeze/archive/$pkgver.tar.gz")
sha512sums=('24ca10e50f118add6fc9bf371e6ce24c17b00285d73963bbd1b3193977e735b26ba7bb314801208d92d6a0eeca0b5d3a661d5aeea317ac22a752cb9b224d6cd3')

build() {
  cd cx_Freeze-$pkgver
  python setup.py build
}

check() {
  cd cx_Freeze-$pkgver
  python setup.py nosetests || warning "Tests failed"
}

package() {
  cd cx_Freeze-$pkgver
  python setup.py install --root "$pkgdir" --optimize 1 --skip-build
}
