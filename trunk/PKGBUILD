$Id$
# Maintainer: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>

pkgname=youtube-viewer
_pkgdir=trizen-youtube-viewer-d4c2653/
epoch=1
pkgver=3.0.2
pkgrel=1
pkgdesc='Command line utility for viewing youtube-videos in MPlayer'
arch=('any')
url="https://github.com/trizen/youtube-viewer"
license=('GPLv3')
source=("$pkgname.zip::https://github.com/trizen/$pkgname/zipball/$pkgver")

depends=('perl>=5.10.0' 'mplayer' 'perl-data-dump' 'perl-libwww' 'perl-xml-fast')
optdepends=('perl-term-readkey: to get the terminal width size'
            'perl-term-readline-gnu: for a better STDIN support'
            'perl-lwp-protocol-https: for login and https support'
            'gcap: for retrieving Youtube closed captions. [in AUR]')


build() {
    cd "$srcdir/$_pkgdir/WWW-YoutubeViewer"

    perl Build.PL --destdir "$pkgdir"   # --gtk-youtube-viewer
    ./Build
}

check(){
    cd "$srcdir/$_pkgdir/WWW-YoutubeViewer"

    ./Build test
}

package() {
    cd "$srcdir/$_pkgdir/WWW-YoutubeViewer"

    ./Build install --install_path script=/usr/bin
}
md5sums=('c411663a0d28db67acbe32761d9b3382')
