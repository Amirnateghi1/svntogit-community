# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lizao (Larry) Li <lzlarryli@gmail.com>

pkgname=lib32-libcurl-gnutls
pkgver=7.50.1
pkgrel=3
pkgdesc='An URL retrieval library'
arch=('x86_64')
url='http://curl.haxx.se'
license=('MIT')
depends=('lib32-glibc' 'lib32-gnutls' 'lib32-nettle' 'lib32-zlib'
         'libcurl-gnutls'
         'libssh2.so')
makedepends=('gcc-multilib')
options=('strip')
source=("http://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc})
sha256sums=('3e392cf600822b817be82d9080b377fcbab70538d5a8bf525a1cd66e157b99ea'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd curl-${pkgver}

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --disable-versioned-symbols \
    --enable-ipv6 \
    --enable-threaded-resolver \
    --without-gssapi \
    --without-libidn \
    --without-ssl \
    --with-gnutls='/usr' \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'

  make -C lib
}

package() {
  cd curl-${pkgver}

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib32/libcurl{,-gnutls}.so.4.4.0
  mv "${pkgdir}"/usr/lib32/libcurl{,-gnutls}.so.4
  rm "${pkgdir}"/usr/lib32/libcurl.{a,so}*
  for version in 3 4.0.0 4.1.0 4.2.0 4.3.0; do
    ln -s libcurl-gnutls.so.4.4.0 "${pkgdir}"/usr/lib32/libcurl-gnutls.so.${version}
  done

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libcurl-gnutls "${pkgdir}"/usr/share/licenses/lib32-libcurl-gnutls
}

# vim: ts=2 sw=2 et:
