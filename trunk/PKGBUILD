# $Id$
# Maintainer: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>

pkgname=xmlrpc-c
pkgver=2222
pkgrel=1
pkgdesc='This library provides a modular implementation of XML-RPC for C and C++'
arch=('i686' 'x86_64')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom:xmlrpc-c')
depends=('curl' 'libxml2' 'gcc-libs' 'zlib')
makedepends=('libtool')
options=('!makeflags' '!libtool')
source=("ftp://ftp.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('5822ea50f50fb86b5f211ec6bd820e5e')

build() {
	[ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"

	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr \
    --mandir=/usr/share/man \
		--enable-libxml2-backend \
		--disable-cgi-server \
		--disable-abyss-server \
		--disable-libwww-client \
		--disable-wininet-client

	make CFLAGS_PERSONAL="${CFLAGS}"
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -m 644 -D doc/COPYING $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}
# vim:set ts=2 sw=2 et:
