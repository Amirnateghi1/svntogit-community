# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Piotr Balcerowski <piotr@balcerowski.org>

pkgname=lib32-libcurl-compat
pkgver=7.50.1
pkgrel=2
pkgdesc='An URL retrieval library (without versioned symbols)'
arch=('x86_64')
url='http://curl.haxx.se'
license=('MIT')
depends=('lib32-glibc' 'lib32-krb5' 'lib32-libidn' 'lib32-openssl' 'lib32-zlib'
         'libcurl-compat'
         'libssh2.so')
makedepends=('gcc-multilib')
options=('strip')
source=("http://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc})
sha256sums=('3e392cf600822b817be82d9080b377fcbab70538d5a8bf525a1cd66e157b99ea'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd curl-${pkgver}

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --disable-versioned-symbols \
    --enable-ipv6 \
    --enable-threaded-resolver \
    --with-gssapi \
    --with-libidn \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'

  make -C lib
}

package() {
  cd curl-${pkgver}

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib32/libcurl{,-compat}.so.4.4.0
  rm "${pkgdir}"/usr/lib32/libcurl.{a,so}*
  for version in 3 4.0.0 4.1.0 4.2.0 4.3.0; do
    ln -s libcurl-compat.so.4.4.0 "${pkgdir}"/usr/lib32/libcurl.so.${version}
  done

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libcurl-compat "${pkgdir}"/usr/share/licenses/lib32-libcurl-compat
}

# vim: ts=2 sw=2 et:
