# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>   

pkgname=mldonkey
pkgver=3.0.4
pkgrel=1
pkgdesc="A multi-network P2P client"
arch=('i686' 'x86_64')
url="http://mldonkey.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'file' 'gd' 'bzip2')
makedepends=('lablgtk2' 'librsvg' 'ocaml')
optdepends=('librsvg: GUI support'
            'libx11: GUI support')
backup=('etc/conf.d/mldonkey')
install=mldonkey.install
source=(http://downloads.sourceforge.net/sourceforge/mldonkey/${pkgname}-${pkgver}.tar.bz2
        mldonkeyd mldonkey.conf mldonkey.desktop
        gtk2_http_proxy.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply a patch taken from the project's bug tracker.
  # The issue is fixed in CVS.  This patch will go away as of
  # the next upstream release.
  patch -p0 < "$srcdir/gtk2_http_proxy.patch"

  ./configure --prefix=/usr --enable-gui=newgui2
  make
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/icons/rsvg/type_source_normal.svg" \
    "${pkgdir}/usr/share/icons/mldonkey.svg"
  install -Dm644 "${srcdir}/mldonkey.desktop" \
    "${pkgdir}/usr/share/applications/mldonkey.desktop"

  install -Dm755 "${srcdir}/mldonkeyd" "${pkgdir}/etc/rc.d/mldonkey"
  install -Dm644 "${srcdir}/mldonkey.conf" "${pkgdir}/etc/conf.d/mldonkey"
}

md5sums=('06a084874dfd99dbad4dabc21e986354'
         'c42526cb7ff703b313728dfdd92dfb1e'
         '190cbbc30b510981e0d8b0ed8bae87a4'
         '1de3266caa49f82bee73ba891eea12cf'
         'e17ca1736818e44532cac6671e59ea3c')
