# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=zanshin
pkgver=0.4.1.git20171127
pkgrel=1
_commit=5d6814b816170d27b23760d76b9e36ea287d81f9
pkgdesc="To-do management application for KDE based on Akonadi"
arch=(x86_64)
url="http://zanshin.kde.org"
license=(GPL)
depends=(kdepim-runtime kontactinterface)
makedepends=(extra-cmake-modules kdoctools boost python krunner kdelibs4support kdesignerplugin git)
#source=("https://files.kde.org/$pkgname/$pkgname-$pkgver.tar.bz2")
source=(git+https://anongit.kde.org/zanshin.git#commit=$_commit)
sha256sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
