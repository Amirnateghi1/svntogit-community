# $Id$
# Maintainer:

pkgname=yaws
pkgver=1.88
pkgrel=3
pkgdesc='Yet Another Web Server -- The pure Erlang HTTP server/framework'
arch=('i686' 'x86_64')
url='http://yaws.hyber.org'
license=('BSD')
depends=('pam' 'erlang')
backup=('etc/yaws/yaws.conf')
source=("http://yaws.hyber.org/download/${pkgname}-${pkgver}.tar.gz")
md5sums=('950f8199592c6490556632e20e59a353')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var

  # python2 fix
  for file in www/cgi.yaws www/cgi-bin/foo.py; do
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/lib/erlang/lib
  ln -s /usr/lib/yaws  ${pkgdir}/usr/lib/erlang/lib/${pkgname}-${pkgver}

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
