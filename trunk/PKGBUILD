# $Id$
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.9.5
pkgrel=5
pkgdesc="Creates DjVu files from PDF files"
arch=('i686' 'x86_64')
url="http://jwilk.net/software/pdf2djvu"
license=('GPL')
depends=('poppler' 'djvulibre' 'gcc-libs' 'graphicsmagick' 'exiv2')
makedepends=('python2-nose' 'ttf-liberation')
source=(https://github.com/jwilk/pdf2djvu/releases/download/$pkgver/pdf2djvu-$pkgver.tar.xz{,.asc}
        0001-configure-enable-the-C-11-standard.patch)
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}

    # Build in C++11 mode; needed when building against poppler >= 0.57.0
    patch -Np1 -i ../0001-configure-enable-the-C-11-standard.patch
    autoreconf -v
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}

    sed -i 's/nosetests/nosetests2/' tests/Makefile
    make test
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
}
md5sums=('4c78d594db304dca7435592eb85e15a4'
         'SKIP'
         '9c32ee3f6a3b8c9316d6825dc6bebe74')
