# Maintainer: David Runge <dave@sleepmap.de>
pkgname=giada
pkgver=0.15.3
pkgrel=1
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=('x86_64')
url="https://www.giadamusic.com/"
license=('GPL3')
groups=('pro-audio')
depends=('fltk' 'jansson' 'libpulse' 'libxpm' 'rtmidi')
makedepends=('gendesk' 'imagemagick')
checkdepends=('catch2' 'xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/monocasual/${pkgname}/archive/v${pkgver}.tar.gz"
        "$pkgname-atomic_include.diff")
sha512sums=('05e928bb228e8cf14816558e8d5981fb1457596a6bbae55ae1b1b224d5dd1f64652cc6310e4771e2b4eb1b3384788c3d485110f89103785494d82ee293a8fe34'
            'dd1164c659e158faabcffe35abd85daf06d1a233c6c017bea6e1221fcf18da2402247757959168bec27252a6fee247939390f1f55b53a938e1a06eb64fb1963f')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Giada \
          --categories "AudioVideo;Audio;Midi;Sequencer"
  # add missing atomic include: https://github.com/monocasual/giada/pull/241
  patch -Np1 -i ../${pkgname}-atomic_include.diff
  # fixing broken catch2 include
  sed -e 's|catch\.hpp|catch2/catch\.hpp|g' -i tests/*.cpp
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --target=linux \
              --enable-system-catch
  make
}

check(){
  cd "$pkgname-$pkgver"
  xvfb-run -a make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # XDG integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "extras/${pkgname}-logo.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
