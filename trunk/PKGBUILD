# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=FanFicFare
pkgname=fanficfare
pkgver=3.13.0
pkgrel=1
pkgdesc="A tool for downloading fanfiction to eBook formats"
arch=('any')
url="https://github.com/JimmXinu/${_pkgname}"
license=('Apache')
changelog=changelog.txt
depends=('python' 'python-beautifulsoup4' 'python-chardet' 'python-html5lib'
         'python-html2text')
optdepends=('python-pillow: support for converting/resizing story images and covers')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('a6ca3dfc165c1fa7ec0f0847131cf888f4247f347dd75194f5bce747bb4223b7')
b2sums=('af6d42f5adfb738be865e09d9f0a07de04fdaecb79b3bb2532b94fcf6221af7996021ce68ed8c38088c2f2f1a4de1d747d7a84f15c5c85da5926e931f727b1a6')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
