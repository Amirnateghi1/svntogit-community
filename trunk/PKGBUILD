# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: jordz <jordz@archlinux.us>

pkgname=nemiver
pkgver=0.9.3
pkgrel=1
pkgdesc="C/C++ debugger for GNOME"
arch=('x86_64' 'i686')
license=('GPL')
url="http://www.gnome.org/projects/nemiver/"
depends=('gdlmm' 'gdb' 'gtksourceviewmm' 'libgtop' 'vte3' 'ghex' 'dconf' 'gsettings-desktop-schemas')
makedepends=('boost' 'intltool' 'gnome-doc-utils')
install=nemiver.install
source=("http://ftp.acc.umu.se/pub/GNOME/sources/nemiver/0.9/$pkgname-$pkgver.tar.xz")
options=('!libtool' '!emptydirs')
sha256sums=('27b8845e54a61ba597050eb16640aaaf2bf21be3191081c9ddd3a34fc2ae617d')

build() {	
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --with-boost \
    --disable-scrollkeeper \
    --enable-symsvis=yes \
    --enable-memoryview=yes \
    --disable-schemas-compile \
    --enable-gsettings=yes
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
