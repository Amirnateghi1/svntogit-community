# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=17.03.1
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

md5sums=('ec90a8611e617b1acd7b049cd45a8425'
         'ef6985e574c2d19aea923b84bf6f38fd'
         '4c1e4efb23eac37604c54d333c77fa5f'
         'ef237135ed862a0d70c21ad1dd43d385'
         '65490ea03fffb26d19eefc93aa4f8e18'
         'ed9796ea07327ff9529f10619c7a8208'
         '28fc12cb0ee29ff328c6d2ae5bc2a94f'
         '1014e6b01adf4d7d77d96b8feaa836bf'
         '78ccf8f00e4c7cb133195ad2a08ec19a'
         '8eeb3c4402d5c702c60565ae33612900')
