# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=zanshin
pkgver=0.3.91
pkgrel=1
pkgdesc="To-do management application for KDE based on Akonadi"
arch=(i686 x86_64)
url="http://zanshin.kde.org"
license=(GPL)
install=$pkgname.install
depends=(kdepim-runtime kldap kontactinterface akonadi-search)
makedepends=(extra-cmake-modules kdoctools boost python krunner)
source=("http://files.kde.org/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('6296855e4ea9629b503257b3d88fb0b4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
