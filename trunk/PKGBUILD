# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>

pkgname=espeak
pkgver=1.49.0
pkgrel=1
pkgdesc='Text to Speech engine for English, with support for other languages'
arch=('x86_64' 'i686')
url='https://github.com/espeak-ng/espeak-ng/'
license=('GPL')
depends=('portaudio' 'libpulse')
# TODO: Add the sonic library to [community] and use it here.
makedepends=('git')
options=('!emptydirs')
source=("git://github.com/espeak-ng/espeak-ng#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd espeak-ng

  ./autogen.sh
}

build() {
  cd espeak-ng

  ./configure --prefix=/usr
  export CFLAGS="$CFLAGS -w --std=c11"
  make -j1
}

package() {
  cd espeak-ng

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
