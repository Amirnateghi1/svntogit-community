# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python2-pytest
pkgver=4.6.8
pkgrel=2
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python2-py' 'python2-setuptools' 'python2-pluggy' 'python2-attrs' 'python2-more-itertools'
         'python2-atomicwrites' 'python2-wcwidth' 'python2-funcsigs' 'python2-pathlib2'
         'python2-importlib-metadata')
makedepends=('python2-setuptools-scm')
checkdepends=('lsof' 'python2-nose' 'python2-mock' 'python2-requests' 'python2-hypothesis'
              # Unlisted, but actually used. Check again after a while:
              'python2-numpy' 'python2-twisted' 'python2-pytest-xdist' 'python2-decorator'
              'python2-jinja' 'python2-pexpect')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz"
        module-rewrite.patch::https://github.com/pytest-dev/pytest/pull/6313.patch)
sha512sums=('b6280ba1dec3f49bb54639d87e22936f655b2e67829e658715e151d70916b826b19cbc43812c17ead748e7927d5aab0b28f1a3a1059548400c71cd3830956230'
            '7491b830529da28e6f60ac29bf7fd219f6281349b97d68d6de97ce657b211abb275b1cca7627877a23e761ca2277bd014a9360ada2f3ca22e482572893b29a1c')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd pytest-$pkgver
  # https://github.com/pytest-dev/pytest/issues/6301
  patch -p1 -i ../module-rewrite.patch
  # Python 2 compatibility
  sed -i 's/yield from _iter_rewritable_modules(new_package_files)/for _module in _iter_rewritable_modules(new_package_files): yield _module/' src/_pytest/config/__init__.py
}

build() {
  cd pytest-$pkgver
  python2 setup.py build
}

check() {
  # Skip test_collect_pyargs_with_testpaths because it mangles PYTHONPATH

  cd pytest-$pkgver
  python2 setup.py egg_info
  PYTHONPATH="$PWD"/build/lib python2 src/pytest.py -k 'not test_collect_pyargs_with_testpaths'
}

package() {
  cd pytest-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/py.test{,2}
  mv "$pkgdir"/usr/bin/pytest{,2}
}
