# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Malte Rabenseifner <malte@zearan.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: FUBAR <mrfubar@gmail.com>

pkgname=('shorewall' 'shorewall6' 'shorewall-core')
pkgver=4.5.1.1
pkgrel=1
arch=('any')
url="http://www.shorewall.net/"
license=('GPL')
source=(http://www.shorewall.net/pub/$pkgname/4.5/$pkgname-4.5.1/$pkgname-$pkgver.tar.bz2
        http://www.shorewall.net/pub/$pkgname/4.5/$pkgname-4.5.1/shorewall6-$pkgver.tar.bz2
        http://www.shorewall.net/pub/$pkgname/4.5/$pkgname-4.5.1/shorewall-core-$pkgver.tar.bz2)
sha256sums=('b1d5a523e77b810a441553e676a2677c722b1ae6803766ae6fd3eb5c73a77a65'
            'c45bbb5e3e3f6df1b6e37f0020de6ad6d82c1596036fd94ee16aedd71f1dee0a'
            '2af5471515f6ee3321848624d565f14a80dad6dd8a60373be9405c9c4ca63b59')

package_shorewall() {
  pkgdesc="An iptables-based firewall for Linux systems"
  depends=('shorewall-core' 'perl-digest-sha1')
  backup=(etc/shorewall/{accounting,actions,blacklist,blrules,clear,ecn,findgw,hosts,init,initdone,interfaces,lib.private,maclist,masq,nat,netmap,notrack,params,policy,providers,proxyarp,refresh,refreshed,restored,routestopped,rtrules,rules,scfilter,secmarks,shorewall.conf,start,started,stop,stopped,tcclasses,tcclear,tcdevices,tcfilters,tcinterfaces,tcpri,tcrules,tos,tunnels,zones})

  cd "$srcdir/$pkgname-$pkgver"

  do_install
}

package_shorewall6() {
  pkgdesc="An iptables-based firewall for Linux systems (with IPv6 support)"
  depends=('shorewall')
  backup=(etc/shorewall6/{accounting,actions,blacklist,blrules,clear,hosts,init,interfaces,maclist,netmap,notrack,params,policy,providers,proxyndp,refresh,refreshed,restored,routestopped,rtrules,rules,scfilter,secmarks,shorewall6.conf,start,started,stop,stopped,tcclasses,tcclear,tcdevices,tcfilters,tcinterfaces,tcpri,tcrules,tos,tunnels,zones})

  cd "$srcdir/$pkgname-$pkgver"

  do_install 6
}

package_shorewall-core() {
  pkgdesc="Core Shorewall libraries"
  depends=('iptables' 'iproute2')

  cd "$srcdir/$pkgname-$pkgver"

  PREFIX="$pkgdir" ./install.sh
}

do_install() {
  local _name_suffix=$1

  sed -i -e 's|^MODULE_SUFFIX=ko$|&.gz|' \
         -e 's|/subsys||' \
      "configfiles/shorewall$_name_suffix.conf"

  cp init.archlinux.sh init.sh
  DEST=/etc/rc.d INIT=shorewall$_name_suffix PREFIX="$pkgdir" ./install.sh

  install -d "$pkgdir/usr/share/doc/shorewall$_name_suffix/"
  cp -r Samples$_name_suffix "$pkgdir/usr/share/doc/shorewall$_name_suffix/"

  chmod 755 "$pkgdir/etc/rc.d/shorewall$_name_suffix"
  chmod -R 644 "$pkgdir/etc/shorewall$_name_suffix/"
  chmod 755 "$pkgdir/etc/shorewall$_name_suffix/"
  chmod 644 "$pkgdir/usr/share/shorewall$_name_suffix/modules"
  chmod 644 "$pkgdir/usr/share/shorewall$_name_suffix/helpers"

  # systemd service file
  install -Dm644 shorewall$_name_suffix.service \
    "$pkgdir/lib/systemd/system/shorewall$_name_suffix.service"
}

# vim:set ts=2 sw=2 et:
