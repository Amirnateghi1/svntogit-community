# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Artyom 'logus' Pervukhin <logus9@gmail.com>
# Contributor: Tim Yang <tdy@gmx.com>

pkgname=paps
pkgver=0.6.8
pkgrel=6
pkgdesc='UTF-8 to PostScript converter via Pango'
url='http://paps.sourceforge.net/'
license=('LGPL')
depends=('pango')
arch=('i686' 'x86_64')
source=("http://downloads.sourceforge.net/paps/paps-${pkgver}.tar.gz"
        'freetype2.patch')
sha1sums=('83646b0de89deb8321f260c2c5a665bc7c8f5928'
          '44b61bcff0d08041eeecdbd93752d44f3b820315')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LDFLAGS=${LDFLAGS/,--as-needed/}
	patch -p1 -i ../freetype2.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --mandir=/usr/share/man
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
