# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Malte Rabenseifner <malte@zearan.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: FUBAR <mrfubar@gmail.com>

pkgname=shorewall
pkgver=4.4.12.1
pkgrel=1
pkgdesc="An iptables-based firewall for Linux systems"
arch=('any')
url="http://www.shorewall.net"
license=('GPL')
depends=('iptables' 'iproute2')
replaces=('shorewall-perl')
backup=(etc/shorewall/{accounting,actions,blacklist,clear,ecn,findgw,hosts,init,initdone,interfaces,isusable,lib.private,maclist,masq,nat,netmap,notrack,params,policy,providers,proxyarp,refresh,refreshed,restored,route_rules,routestopped,rules,shorewall.conf,start,started,stop,stopped,tcclasses,tcclear,tcdevices,tcfilters,tcinterfaces,tcpri,tcrules,tos,tunnels,zones})
source=(http://www.shorewall.net/pub/$pkgname/4.4/$pkgname-4.4.12/$pkgname-$pkgver.tar.bz2)
md5sums=('0b61326ee735db5971408e53fc3d5d67')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i -e 's|LOGFILE=/var/log/messages|LOGFILE=/var/log/messages.log|' \
         -e 's|/subsys||' \
      configfiles/shorewall.conf

  cp init.archlinux.sh init.sh
  DEST=/etc/rc.d INIT=shorewall PREFIX="$pkgdir" ./install.sh
  cp -r "$srcdir/$pkgname-$pkgver/Samples" "$pkgdir/usr/share/shorewall/"

  chmod 755 "$pkgdir/etc/rc.d/shorewall"
  chmod -R 644 "$pkgdir/etc/shorewall/"
  chmod 755 "$pkgdir/etc/shorewall/"
  chmod 644 "$pkgdir/usr/share/shorewall/modules"
  chmod 644 "$pkgdir/usr/share/shorewall/helpers"
}

# vim:set ts=2 sw=2 et:
