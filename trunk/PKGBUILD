# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=pynac
pkgver=0.7.0
pkgrel=2
pkgdesc="Python version of GiNaC, a C++ library for symbolic mathematical calculations"
arch=(i686 x86_64)
url="http://pynac.org"
license=(GPL)
depends=(python2 libgiac flint)
source=("https://github.com/pynac/pynac/archive/$pkgname-$pkgver.tar.gz"
        flint-linking.patch::"https://github.com/pynac/pynac/commit/870fee7b.patch")
md5sums=('1df469bcf4240bfe451f3804adba8f1a'
         '7a17f53a184a8d4b36fdb9d3446e374c')

prepare() {
  cd pynac-pynac-$pkgver
# Fix underlinking
  patch -p1 -i ../flint-linking.patch
}

build() {
  cd pynac-pynac-$pkgver

  export PYTHON=python2
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd pynac-pynac-$pkgver
  make DESTDIR="$pkgdir" install
}
