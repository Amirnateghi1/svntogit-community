# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=terraform-provider-libvirt
pkgver=0.6.2
pkgrel=3
pkgdesc="Terraform provider to provision infrastructure with Linux's KVM using libvirt"
arch=("x86_64")
url="https://github.com/dmacvicar/terraform-provider-libvirt"
license=('Apache')
depends=('glibc' 'terraform')
makedepends=('go' 'git' 'libvirt')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dmacvicar/${pkgname}/archive/v$pkgver.tar.gz"
        0001-Fix-channel-type-for-qemu-guest-agent.patch)
sha512sums=('9855fe2d0b7a827508dd41d779f1f160b1d230bf6ee1eeb7ae58418fffbd250899382e315a8ababdf0c89da21c011f874ead1d6f53a723290851e1bc47384c1d'
            '10ac1e8350f857d4b2a5b23fbfa4f8af48516a545300b57cb89c046bf0b4fb0889f4d7e21f8c93412aab08f68e3b94a7ffb623b1795a62d25d6a0b46e26671d9')
b2sums=('ce150963be678dda1a4b11e3707140b7b819b8fe6e01799a769c2da737f6280d7eca64ac704c41426737a3a8432fd8c42f3c4d6c00a14cf291e123c4378842bd'
        'a9d6a4a79cea2a555e45a62687d00decd9d3a656af85a0108ff149b7be495d0b85075bc6ecc89113c9f843e4bc9e0fb29f195f4835b3519bac5cb587ea93f1e4')

prepare() {
  cd "${pkgname}-${pkgver}"
  # let provider create the correct channel type for qemu-guest-agent:
  # https://github.com/dmacvicar/terraform-provider-libvirt/pull/801
  patch -Np1 -i ../0001-Fix-channel-type-for-qemu-guest-agent.patch
  mkdir -vp build
}

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o "build/${pkgname}"
}

package() {
  depends+=('libvirt.so' 'libvirt-lxc.so' 'libvirt-qemu.so')
  cd "${pkgname}-${pkgver}"
  # terraform unfortunately only accepts non-FHS compliant directories for plugins :(
  install -vDm 755 "build/$pkgname" \
    "$pkgdir/usr/share/terraform/plugins/registry.terraform.io/dmacvicar/libvirt/${pkgver}/linux_amd64/${pkgname}"
  install -vDm 644 {CHANGELOG,CONTRIBUTING,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # examples
  find examples/ -type f \( \
      -iname "*.cfg" -or \
      -iname "*.conf" -or \
      -iname "*.md" -or \
      -iname "*.mount" -or \
      -iname "*.service" -or \
      -iname "*.tf" -or \
      -iname "*.xsl" -or \
      -iname "*id_rsa*" \
    \) -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
  find website/ -type f -iname "*.markdown" -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
}
