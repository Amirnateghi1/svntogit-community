# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=Kvantum
pkgname=kvantum-qt5
pkgver=0.12.1
pkgrel=1
pkgdesc="SVG-based theme engine for Qt5 (including config tool and extra themes)"
arch=(x86_64)
url="https://github.com/tsujan/Kvantum"
license=(GPL)
depends=(qt5-base qt5-svg qt5-x11extras libx11 libxext hicolor-icon-theme kwindowsystem)
makedepends=(cmake qt5-tools)
source=("${url}/archive/V${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('a12cc341eb5fd75cf592d1e99623ad0692d96b1ab22c2e52260f978524a12358')

prepare() {
    mkdir -p build
}

build() {
    cd build
	cmake ../${_pkgname}-${pkgver}/${_pkgname} \
	      -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
    # KDE4 is long dead
    rm -r "${pkgdir}"/usr/share/kde4
}
