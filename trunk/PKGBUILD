# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=firefox-dark-reader
pkgver=4.9.26
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
url=https://darkreader.org/
arch=('any')
license=('MIT')
groups=('firefox-addons')
makedepends=('nodejs' 'npm')
source=("https://github.com/darkreader/darkreader/archive/v$pkgver/darkreader-v$pkgver.tar.gz")
b2sums=('9ca8df8eb5f695ee8ee2d4694afa170ca206e5d4468370dc24b3704d6313e4f45939faff398f48df48125b6411b7f74e241d47680ea358dbf52f12e6ac8811d5')

build() {
  cd darkreader-$pkgver
  npm install
  npm run build
}

check() {
  cd darkreader-$pkgver
  npm test
}

package() {
  cd darkreader-$pkgver
  install -Dm644 build-firefox.xpi "$pkgdir"/usr/lib/firefox/browser/extensions/addon@darkreader.org.xpi
}

# vim:set ts=2 sw=2 et:
