# Maintainer: David Runge <dave@sleepmap.de>

pkgname=redkite
pkgver=0.6.1
pkgrel=1
pkgdesc="A small free software and cross-platform GUI toolkit"
arch=('x86_64')
url="https://github.com/quamplex/redkite"
license=('GPL3')
depends=('cairo')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/quamplex/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('2ba9e9becc79a08a26fd414324d282516fff75f0f2d3c1adabe52669fcafe93c0d89fd40276bf0a42248d6eec5972a58901a9e4853b3b6c9f8334565b186994a')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -pv build
}

build() {
  cd "$pkgname-$pkgver"
  (
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          ..
    make VERBOSE=1
  )
}

package() {
  cd "$pkgname-$pkgver"
  (
    cd build
    make DESTDIR="$pkgdir/" install
  )
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
