# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=19.01
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('8d061db9c2dc7e0f20fcc4a03075359258b8ca60c7431b2b40c4585aafd783dc1624a7aa0485150f69b879048d6c85cfdbe9c3c34f1bcaabce0258fcc5617b37'
            '05a0de98b46c64fc4de15c7c5a9e5522318146804ab6ec7e6f7f28b005abd6cd1b4e5fd192e59dd490065ef0dd8f20b96e5e290a67d5c9d6b53b00d11246b29d'
            'bc586b611b4c9948aefab6e2e7ac05079207d8a83c535a3a99e612815030dd846f1f93dd1de4cee6c2f581fb9129d9ebfca2731042a5eb1d44dc2964d9d482db'
            'da12a1f8f0892851cec8311c8f0501a80adbe5766a69f60f95b53998ef34ccc6e2f1652a2a8233abc2505bc1f09654517d63410f7ba15c409c2845a4aa8df886')
