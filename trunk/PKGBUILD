# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Timm Preetz <timm@preetz.us>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Dave Pretty <david dot pretty at gmail dot com>

pkgname=anki
pkgver=2.1.0a10
pkgrel=1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently"
url="http://ankisrs.net/"
license=('AGPL3')
arch=('any')
depends=('qt5-webengine' 'python-beautifulsoup4' 'python-pyaudio' 'python-pyqt5' 'python-requests' 'python-send2trash')
optdepends=('lame: record sound'
            'mplayer: play sound')
source=(https://apps.ankiweb.net/downloads/alpha/alpha10/anki-$pkgver-source.tgz)
sha256sums=('1b614e2ff7ae52752e1d0481e328c10c73cee26faf1d8c2f288806e726698923')

package() {
  cd "$srcdir"/anki-$pkgver
  mkdir -p "$pkgdir"/usr/{bin,share/{anki,pixmaps,applications,man/man1}}
  cp -av * "$pkgdir"/usr/share/anki/
  cd "$pkgdir"/usr/share/anki && (
    mv tools/runanki.system ../../bin/anki
    mv anki.xpm anki.png ../pixmaps/
    mv anki.desktop ../applications/
    mv anki.1 ../man/man1/
  )
  rm -rf "$pkgdir"/usr/share/anki/{tests,thirdparty/[!s]*,tools,anki.bat}
}
