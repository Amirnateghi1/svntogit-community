# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Guillaume Duboc <guilduboc@gmail.com>

pkgname=jupyterlab_server
pkgver=1.0.7
pkgrel=1
pkgdesc="Launch an application built using JupyterLab"
arch=(any)
url="https://jupyter.org/"
license=(custom)
depends=(python-json5 jupyter-notebook)
makedepends=(python-setuptools)
source=(https://pypi.io/packages/source/j/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('12381712f2e70b68442c03046b3afa6483dad4ccae9f47673ffe8a808cefd8e2')

build() {
  cd $pkgname-$pkgver
  python setup.py build 
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
