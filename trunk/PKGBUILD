# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Malte Rabenseifner <malte@zearan.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: FUBAR <mrfubar@gmail.com>

pkgname=('shorewall' 'shorewall6')
pkgver=4.4.27.2
pkgrel=1
arch=('any')
url="http://www.shorewall.net/"
license=('GPL')
source=(http://www.shorewall.net/pub/$pkgname/4.4/$pkgname-4.4.27/$pkgname-$pkgver.tar.bz2
        http://www.shorewall.net/pub/$pkgname/4.4/$pkgname-4.4.27/shorewall6-$pkgver.tar.bz2)
sha256sums=('6e2402ac9e24976a1643da12e8a1c0367359766099dc7e794e4410b7d104facb'
            '1078e6987dfe6939d93be000f41a833cb5b88bb98bb1f4ea45ad32068b3bab01')

package_shorewall() {
  pkgdesc="An iptables-based firewall for Linux systems"
  depends=('iptables' 'iproute2')
  backup=(etc/shorewall/{accounting,actions,blacklist,blrules,clear,ecn,findgw,hosts,init,initdone,interfaces,isusable,lib.private,maclist,masq,nat,netmap,notrack,params,policy,providers,proxyarp,refresh,refreshed,restored,route_rules,routestopped,rules,scfilter,secmarks,shorewall.conf,start,started,stop,stopped,tcclasses,tcclear,tcdevices,tcfilters,tcinterfaces,tcpri,tcrules,tos,tunnels,zones})

  cd "$srcdir/$pkgname-$pkgver"

  do_install
}

package_shorewall6() {
  pkgdesc="An iptables-based firewall for Linux systems (with IPv6 support)"
  depends=('shorewall')
  backup=(etc/shorewall6/{accounting,actions,blacklist,blrules,clear,hosts,init,interfaces,isusable,maclist,notrack,params,policy,providers,proxyndp,refresh,refreshed,restored,route_rules,routestopped,rules,scfilter,secmarks,shorewall6.conf,start,started,stop,stopped,tcclasses,tcclear,tcdevices,tcfilters,tcinterfaces,tcpri,tcrules,tos,tunnels,zones})

  cd "$srcdir/shorewall6-$pkgver"

  do_install 6
}

do_install() {
  local _name_suffix=$1

  sed -i -e 's|^LOGFILE=/var/log/messages$|&.log|' \
         -e 's|^MODULE_SUFFIX=ko$|&.gz|' \
         -e 's|/subsys||' \
      "configfiles/shorewall$_name_suffix.conf"

  cp init.archlinux.sh init.sh
  DEST=/etc/rc.d INIT=shorewall$_name_suffix PREFIX="$pkgdir" ./install.sh

  install -d "$pkgdir/usr/share/doc/shorewall$_name_suffix/"
  cp -r Samples$_name_suffix "$pkgdir/usr/share/doc/shorewall$_name_suffix/"

  chmod 755 "$pkgdir/etc/rc.d/shorewall$_name_suffix"
  chmod -R 644 "$pkgdir/etc/shorewall$_name_suffix/"
  chmod 755 "$pkgdir/etc/shorewall$_name_suffix/"
  chmod 644 "$pkgdir/usr/share/shorewall$_name_suffix/modules"
  chmod 644 "$pkgdir/usr/share/shorewall$_name_suffix/helpers"

  # systemd service file
  install -Dm644 shorewall$_name_suffix.service \
    "$pkgdir/lib/systemd/system/shorewall$_name_suffix.service"
}

# vim:set ts=2 sw=2 et:
