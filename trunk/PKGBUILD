# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=17.05
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

md5sums=('5a020002fab19f20d1baacd50c1ad076'
         'dc645cc88a6856e87969ead469010493'
         'b35574fe230757f9e755db079526b283'
         'da761d0f9f56407407a9943fb48cc8d2'
         '9356a825607ed95886ff570e71162608'
         '380b7c9a1974e6b2bf96868e0f4c41ee'
         'e0d4f89e2fe89812c4417cd5884dd878'
         '3cd0a0656a7bbc04834f212e4396b15f'
         'fefe0a9127d2d176a6f9a3e3a2014762'
         '659d9a32bbee305668bec5afbe97c821')
