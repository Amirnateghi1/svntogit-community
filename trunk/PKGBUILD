# $Id$
# Maintainer: Evgeniy Alekseev <arcanis.arch at gmail dot com>
# Contributor: Matt Harrison <matt at mistbyte dot com>
# Contributor: Muhammad Qadri <Muhammad dot A dot Qadri at gmail dot com>

pkgname=julius
pkgver=4.4
pkgrel=1
pkgdesc="A high-performance, two-pass large vocabulary continuous speech recognition decoder software"
arch=("i686" "x86_64")
url="http://julius.sourceforge.jp/"
license=("custom")
depends=("libpulse" "sdl2")
optdepends=("voxforge-am-julius: acoustic models")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/julius-speech/julius/archive/v${pkgver}.tar.gz"
        "avx-compilation-p1.patch::https://github.com/julius-speech/julius/commit/80fb587284575c6925a4344a2867301c7ba085a2.patch"
        "avx-compilation-p2.patch::https://github.com/julius-speech/julius/commit/ec71ead6c5798fe1e89438cfcf750625f643f9cb.patch"
        "configure.patch")
md5sums=('4724b259adef4160eb3b1fe864d88768'
         '9f8a5f15103bc3eae21d9fa87c8323f1'
         'ac2d405f4355f88076a12782cafe8c47'
         '1b9291772f3d79f85e622f79d650e3cf')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # see https://github.com/julius-speech/julius/issues/36
  patch -p1 -i "${srcdir}/avx-compilation-p1.patch"
  patch -p1 -i "${srcdir}/avx-compilation-p2.patch"

  # configure error
  patch -p1 -i "${srcdir}/configure.patch"
}

build() {
  # redefine makeflags, coz it could not build in parallel
  export MAKEFLAGS="-j1"

  cd "${srcdir}/${pkgname}-${pkgver}/"
  ./configure \
    --prefix="/usr/" \
    --mandir='${prefix}/share/man'\
    --enable-words-int \
    --enable-msd \
    --enable-gmm-vad \
    --enable-decoder-vad \
    --enable-power-reject \
    --enable-wpair
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  # leave static libraries since julius does not support
  # shared ones at the moment. See also FS#50599
  make prefix="${pkgdir}/usr" install
  install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/julius/LICENSE"
}

# vim:set ts=2 sw=2 et:
