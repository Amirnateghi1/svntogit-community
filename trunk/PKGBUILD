# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/

pkgname=udevil
pkgver=0.4.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Mount and unmount without password'
url='http://ignorantguru.github.com/udevil/'
license=('GPL3')
makedepends=('intltool' 'gettext')
depends=('udev' 'glib2')
optdepends=('davfs2:     mount WebDAV shares'
            'nfs-utils:  mount nfs shares'
            'sshfs:      mount sftp shares'
            'curlftpfs:  mount ftp shares'
            'cifs-utils: mount samba shares'
            'eject:      eject via devmon'
            'zenity:     devmon popups'
            'udisks:     devmon mount without suid udevil'
            'udisks2:    devmon mount without suid udevil')
provides=('devmon')
backup=('etc/udevil/udevil.conf')
install=$pkgname.install
source=(https://raw.github.com/IgnorantGuru/$pkgname/master/packages/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('baaab9b65ccbab55849ce4dec22ba412fd45655b14d163030c2245bc62fc7265')

build() {
    cd $srcdir/$pkgname-$pkgver
    ./configure --prefix=/usr
    make
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install
}

