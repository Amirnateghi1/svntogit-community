# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=mldonkey
pkgver=3.1.3
pkgrel=3
pkgdesc='A multi-network P2P client'
arch=(i686 x86_64)
url='http://mldonkey.sourceforge.net/'
license=(GPL)
depends=(desktop-file-utils file gd)
makedepends=(lablgtk2 librsvg ocaml)
optdepends=('librsvg: GUI support'
            'gtk2: GUI support')
backup=(etc/conf.d/mldonkey)
install=mldonkey.install
source=(http://downloads.sourceforge.net/sourceforge/mldonkey/$pkgname-$pkgver.tar.bz2
        mldonkey.conf
        mldonkey.service
        mldonkey.tmpfiles
        gd_version.patch)
sha256sums=('7c259f9f41cc12899045710ddce02e6d25962b2cb5c22f9cf58af0483f06d9e2'
            'f1d9401cefd591662d49011c53fdb2788755a6f745a963e46d8037b990edeb6a'
            '778cebe8edcffd63db3594054c2daa62ce571644a96ad235b8c95470b55c0415'
            '45f45d2a3c064d5bbc0b6b9b70fa3497ed47cfecdd1ec52e175311f48eaa89dc'
            '0b6f523848790627fb8020dae788e33132bbb7fdd76d6385b012b1b0ad7a2457')

prepare() {
  cd mldonkey-$pkgver
  patch -p0 < "$srcdir/gd_version.patch" # a fix from upstream http://cvs.savannah.gnu.org/viewvc/mldonkey/config/configure.in?root=mldonkey&r1=1.357&r2=1.358&view=patch

  rm config/configure  # remove this file to force its regeneration, we've just changed ./config/configure.in
}

build() {
  cd mldonkey-$pkgver
  ./configure --prefix=/usr --enable-gui=newgui2
  make
}

package() {
  cd mldonkey-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 icons/rsvg/type_source_normal.svg "$pkgdir"/usr/share/icons/mldonkey.svg
  install -Dm644 distrib/mldonkey.desktop "$pkgdir"/usr/share/applications/mldonkey.desktop

  install -Dm644 "$srcdir"/mldonkey.conf "$pkgdir"/etc/conf.d/mldonkey
  install -Dm644 "$srcdir"/mldonkey.service "$pkgdir"/usr/lib/systemd/system/mldonkey.service
  install -Dm644 "$srcdir"/mldonkey.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/mldonkey.conf
}
