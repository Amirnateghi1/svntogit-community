# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=18.10
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('413420a726bce2c830c79ac7c24568fba803a3879df928ee2c1fb95e6594a545eb6b0aa06c8ef51f678be22ece115b9b01e28bc85367b171e4b55babc33bedf3'
            '529fd5d2c4cac90ce9b70018c70f1793af8730bc1fbe8ef64d1368984c6758a541f27f0c8ee316c28404926dacba39dfc1a914693572ee1b96452d385c5e4015'
            '28bf33207909ec4f3d3fe4ff8187924588d300aa5a5c112417e27da37e33371b55bed6e4f577c3576e7660e1163ef814381944e471b38ab3b6e29a5eb2cd3ef3'
            'c7bd8092d3354d524b0008128640f3ed3d7be742b4b6f1e60a6904a7f68421e2ddb854563acd20300b8b5ea9a44b8efb4e73b4b3de406b9ff07ce551257a41ac')
