# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=redkite
pkgver=1.0.2
pkgrel=1
pkgdesc="A small free software and cross-platform GUI toolkit"
arch=('x86_64')
url="https://gitlab.com/iurie-sw/redkite"
license=('GPL3')
depends=('glibc' 'cairo')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/iurie-sw/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('783649ac773fbb72c8e4cd0e717946f7b8cee32a3f7a7b9e6e34472ccc10760d3bc05c0170f65f3de7b60aff81c9af47444537bea9394dde9d30e8d514626ad9')
b2sums=('6179d499e39eb9492195172864e7c5224f834c3fa4f86701037608be26674eadf47d785cfed4ad10e8ccc43c62060f35a7cf18096ad8006639d5744e275ca54f')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
