# Maintainer: Alad Wenter <alad@archlinux.org>
# Contributor: John Jenkins twodopeshaggy@gmail.com
pkgname=rtv
pkgver=1.25.0
pkgrel=1
pkgdesc="Browse Reddit from your terminal"
arch=('any')
url="https://github.com/michael-lazar/rtv"
license=('custom:MIT')
makedepends=('python-setuptools')
depends=('python-beautifulsoup4' 'python-decorator' 
         'python-kitchen' 'python-six' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('253455ccfc0d18c1f48465bf69a036ba4e3fd64267b1709a0aaef3419e3344e7')

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
