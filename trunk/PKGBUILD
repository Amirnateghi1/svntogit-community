# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# Contributor: Teo Mrnjavac <teo@kde.org>
# https://github.com/zizzfizzix/pkgbuilds

pkgname=qtkeychain
pkgver=0.4
pkgrel=1
pkgdesc='Provides support for secure credentials storage'
arch=('i686' 'x86_64')
url='https://github.com/frankosterfeld/qtkeychain'
license=('BSD')
depends=('qt5-base')
makedepends=('cmake' 'icu' 'qt5-tools')
provides=('qtkeychain-qt5')
replaces=('qtkeychain-qt5')
source=("https://github.com/frankosterfeld/qtkeychain/archive/${pkgver}.zip")
md5sums=('256e3606b37a2f123491ab31d4521ba0')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../$pkgname-$pkgver \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
          -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
    install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
