# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.9.15
pkgrel=2
pkgdesc="Creates DjVu files from PDF files"
arch=('x86_64')
url="https://jwilk.net/software/pdf2djvu"
license=('GPL')
depends=('poppler' 'djvulibre' 'gcc-libs' 'graphicsmagick' 'exiv2')
makedepends=('python2-nose' 'ttf-liberation' 'boost')
source=(https://github.com/jwilk/pdf2djvu/releases/download/$pkgver/pdf2djvu-$pkgver.tar.xz{,.asc}
        $pkgname-poppler-0.85.patch::https://github.com/jwilk/pdf2djvu/commit/b3ccb5fcc7.patch)
sha256sums=('d27a9f4e358486e1914d1e0fd701f861255836d0ea4f8616e6bd4efbf859b56b'
            'SKIP'
            '4f3b9b9124432d1a5dd6e38a3032cd8420257aaa0ef21f416341c25060004582')
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -Np1 -i ../$pkgname-poppler-0.85.patch
}

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}

   make PYTHON=python2 test
}

package() {
    cd ${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
}
