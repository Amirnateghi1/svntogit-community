# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>
pkgname=xmlrpc-c
pkgver=1.16.39
epoch=1
pkgrel=1
pkgdesc='XML-RPC for C and C++'
arch=('x86_64' 'i686')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl' 'libxml2' 'gcc-libs' 'zlib')
makedepends=('libtool' 'setconf')
options=('!makeflags' '!libtool')
source=("http://sourceforge.net/projects/$pkgname/files/Xmlrpc-c%20Super%20Stable/$pkgver/$pkgname-$pkgver.tgz")
sha256sums=('54adc6dae0af044fcbbfe97625f256f8afe7d7f0768ef82cef1f98d03c3fbb39')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ "$CARCH" != "i686" ] && export CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-libxml2-backend \
    --disable-cgi-server \
    --disable-abyss-server \
    --disable-libwww-client \
    --disable-wininet-client \
    --disable-curl-client
  make CFLAGS_PERSONAL="$CFLAGS"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # Remove references to build directory
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" BLDDIR ""
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" ABS_SRCDIR ""
}

# vim:set ts=2 sw=2 et:
