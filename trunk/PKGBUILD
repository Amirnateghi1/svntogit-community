# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=ipython2
_pkgbase=ipython
pkgname=(ipython2 ipython2-docs)
pkgver=0.11
pkgrel=4
arch=('any')
url="http://ipython.org"
license=('Modified BSD')
makedepends=('python2') # for setup.py
source=("http://archive.ipython.org/release/$pkgver/ipython-$pkgver.tar.gz")
md5sums=('efc899e752a4a4a67a99575cea1719ef')

build() {
  true
  # There's nothing to do here.  No configure, make, et cetera.  setup.py
  # does it all.
}

package_ipython2() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('python2')
  optdepends=("wxpython: needed for ipythonx and ipython-wx"
              "twisted: networking-related tasks"
              "python-foolscap: for IPython's parallel computing features"
              "python-pexpect: for irunner"
              "python2-nose: if you want to run IPython's test suite"
              "python2-pyqt: for ipython-qtconsole"
              "python2-sip: for ipython-qtconsole"
              "python2-pygments: for ipython-qtconsole"
              "python2-pyzmq: for ipython-qtconsole")

  cd "$srcdir/$_pkgbase-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  rm -rf "$pkgdir/usr/share/doc"
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  mv ipcluster.1.gz    ipcluster2.1.gz
  mv ipcontroller.1.gz ipcontroller2.1.gz
  mv ipengine.1.gz     ipengine2.1.gz
  mv iplogger.1.gz     iplogger2.1.gz
  mv ipython.1.gz      ipython2.1.gz
  mv irunner.1.gz      irunner2.1.gz
  mv pycolor.1.gz      pycolor2.1.gz
  cd "$pkgdir/usr/bin/"
  mv ipcluster         ipcluster2
  mv ipcontroller      ipcontroller2
  mv ipengine          ipengine2
  mv iplogger          iplogger2
  mv iptest            iptest2
  mv ipython           ipython2
  mv irunner           irunner2
  mv pycolor           pycolor2
}

package_ipython2-docs() {
  pkgdesc='Documentation and examples for IPython2'

  cd "$srcdir/$_pkgbase-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir"
  rm -rf "$pkgdir/usr/lib" "$pkgdir/usr/bin" "$pkgdir/usr/share/man"
  # This seems wrong.  We're running setup.py for both
  # packages, and removing different things in each.
  mv "$pkgdir/usr/share/doc/ipython/" "$pkgdir/usr/share/doc/ipython2/"
}

