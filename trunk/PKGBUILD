# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>

pkgname=xmlrpc-c
# The webpage is unclear on which version is the latest advanced version (2012-04-13)
# Both the 1.29x series and 1.30.1 are mentioned. I'll go with 1.29.2 for now.
pkgver=1.29.2
epoch=1
pkgrel=2
pkgdesc='XML-RPC for C and C++'
arch=('x86_64' 'i686')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl' 'libxml2' 'gcc-libs' 'zlib')
makedepends=('libtool' 'setconf')
options=('!makeflags' '!libtool')
source=("$pkgname-advanced-$pkgver.tar.gz::http://xmlrpc-c.svn.sourceforge.net/viewvc/xmlrpc-c/advanced.tar.gz")
sha256sums=('68f5ae842a490778899f51a7ebec3399b9238f28db94b764646202c48bd8381f')

build() {
  cd "$srcdir/advanced"

  [ "$CARCH" != "i686" ] && export CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-libxml2-backend \
    --disable-cgi-server \
    --disable-abyss-server \
    --disable-libwww-client \
    --disable-wininet-client
  mkdir include/curl
  touch include/curl/types.h
  make CFLAGS_PERSONAL="$CFLAGS"
}

package() {
  cd "$srcdir/advanced"

  make DESTDIR="$pkgdir" install
  install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # Remove references to build directory
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" BLDDIR ""
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" ABS_SRCDIR ""
}

# vim:set ts=2 sw=2 et:
