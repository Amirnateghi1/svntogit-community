# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Denis Terskov aka neurosurgeon <terskov.den@gmail.com>

pkgname=scantailor
pkgver=0.9.12.2
pkgrel=2
_realver=EXPERIMENTAL_2016_02_22
pkgdesc="Interactive post-processing tool for scanned pages"
arch=(x86_64)
url="http://scantailor.org/"
license=("GPL")
depends=('qt5-base' 'libtiff' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools' 'eigen' 'boost')
source=($pkgname-$_realver.tar.gz::https://github.com/Tulon/$pkgname/archive/$_realver.tar.gz
        scantailor.desktop)
sha256sums=('157f5cae617041eefa84c41e87bc18747bafc73eead16f73c2c6c0a717a82fab'
            '3c191c0380f991709bd38a0ace6adbf351fca441a2c5fd432e56a0da6b68a728')

build() {
  cd "$srcdir"/$pkgname-$_realver
  cmake \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	.
  make
}

package() {
  cd "$srcdir"/$pkgname-$_realver
  make DESTDIR="$pkgdir" install
  install -Dm0644 "resources/appicon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/scantailor.svg"
  install -Dm0644 "$srcdir/scantailor.desktop" "$pkgdir/usr/share/applications/scantailor.desktop"
}
