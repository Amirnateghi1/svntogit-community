# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=libwebsockets
pkgver=3.2.2
pkgrel=2
pkgdesc="C library for websocket clients and servers"
arch=('x86_64')
url="https://libwebsockets.org"
depends=('openssl' 'libcap')
makedepends=('cmake')
license=('LGPL')
source=($pkgname-$pkgver.tar.gz::https://github.com/warmcat/libwebsockets/archive/v$pkgver.tar.gz)
sha256sums=('166d6e17cab64bfc10c2a71799c298284540a1fa63f6ea3de5caccb34502243c')

build() {
  cd "$pkgname-$pkgver"

  mkdir build && cd build
  # http2 support -DLWS_WITH_HTTP2=1
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DLWS_WITH_STATIC=OFF \
        -DLWS_LINK_TESTAPPS_DYNAMIC=ON \
        -DLWS_UNIX_SOCK=ON \
        -DLWS_IPV6=ON \
        ..
#-DLWS_WITHOUT_TEST_SERVER=ON \  # Disable certificate generation
  make
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd build
  make DESTDIR="${pkgdir}" install
}
