# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=redkite
pkgver=0.8.1
pkgrel=1
pkgdesc="A small free software and cross-platform GUI toolkit"
arch=('x86_64')
url="https://gitlab.com/geontime/redkite"
license=('GPL3')
depends=('glibc' 'cairo')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/geontime/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('e0df59a1fbf4bd68ee50eb8225962ecd7a83a11660b9b8528e4188144c86c45223df09797c11f087e7834ca79394fe50ddb447a14c5f421a227ce39c5095cf33')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
