# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=libwebsockets
pkgver=4.0.21
pkgrel=1
pkgdesc="C library for websocket clients and servers"
arch=('x86_64')
url="https://libwebsockets.org"
license=('MIT')
depends=('glibc' 'openssl' 'libuv' 'libev' 'zlib')
makedepends=('cmake' 'glib2' 'libcap')
provides=('libwebsockets.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/warmcat/libwebsockets/archive/v$pkgver.tar.gz")
sha512sums=('03a32a3521eb4b060905fced793ce21ca94a8fa0465ca2c5fce1db1978052045ddd1ea252ef4a5ad488a24682bc44b8572331d978c5da53382d457e265db7d94')
b2sums=('9c3929d974af7694e75dec6dfd622b1bff20a62d152dd7709c0b325c59f52ff0ebdc912c9e71b098ba4ca7d29a2e765e0b0e9894541dcf9c84fdc325b93a58fc')

build() {
  cd "$pkgname-$pkgver"
  cmake -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_BUILD_TYPE='None' \
        -D LWS_WITH_HTTP2=ON \
        -D LWS_IPV6=ON \
        -D LWS_WITH_GLIB=ON \
        -D LWS_WITH_ZIP_FOPS=ON \
        -D LWS_WITH_SOCKS5=ON \
        -D LWS_WITH_RANGES=ON \
        -D LWS_WITH_ACME=ON \
        -D LWS_WITH_LIBUV=ON \
        -D LWS_WITH_LIBEV=ON \
        -D LWS_WITH_LIBEVENT=OFF \
        -D LWS_WITH_FTS=ON \
        -D LWS_WITH_THREADPOOL=ON \
        -D LWS_UNIX_SOCK=ON \
        -D LWS_WITH_HTTP_PROXY=ON \
        -D LWS_WITH_DISKCACHE=ON \
        -D LWS_WITH_LWSAC=ON \
        -D LWS_LINK_TESTAPPS_DYNAMIC=ON \
        -D LWS_WITHOUT_BUILTIN_GETIFADDRS=ON \
        -D LWS_WITHOUT_BUILTIN_SHA1=ON \
        -D LWS_WITH_STATIC=OFF \
        -D LWS_WITHOUT_CLIENT=OFF \
        -D LWS_WITHOUT_SERVER=OFF \
        -D LWS_WITHOUT_TESTAPPS=ON \
        -D LWS_WITHOUT_TEST_SERVER=OFF \
        -D LWS_WITHOUT_TEST_SERVER_EXTPOLL=ON \
        -D LWS_WITHOUT_TEST_PING=ON \
        -D LWS_WITHOUT_TEST_CLIENT=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libcap.so' 'libglib-2.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
