# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: poisonby <poisonby@tutanota.com>
# Contributor: Jason Scurtu (scujas) <jscurtu@gmail.com>
# Contributor: Marcin Tydelski <marcin.tydelski@gmail.com>
# Contributor: Jan Lukas Gernert (JeanLuc) <https://launchpad.net/~eviltwin1>

pkgname=feedreader
_pkgname=FeedReader
pkgver=2.0.2+548+g08dfb5db
pkgrel=1
pkgdesc="Modern desktop application designed to complement existing web-based RSS accounts"
arch=(x86_64)
url="https://jangernert.github.io/FeedReader/"
license=(GPL3)
depends=(curl gnome-online-accounts libgee libpeas)
makedepends=(cmake git gobject-introspection vala)
_commit=08dfb5dbcf1e04219ae8717dd55449d497e0eba4  # master
source=("git+https://github.com/jangernert/$_pkgname#commit=$_commit"
        "git+https://github.com/fivefilters/ftr-site-config"
        "locale.patch")
sha256sums=('SKIP'
            'SKIP'
            '65be277a3d7b775cb03b1db06d1e4de0bd5d3c8484308ca2fa486ffb03be2346')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname
  git config submodule.data/GrabberConfig.url "$srcdir/ftr-site-config"
  git submodule update --init
  mkdir build

  # Fix parsing date on non English locales
  patch -Np1 -i ../locale.patch
}

build() {
  cd $_pkgname/build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd $_pkgname/build
  make DESTDIR="$pkgdir" install
}
