# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Malte Rabenseifner <malte@zearan.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: FUBAR <mrfubar@gmail.com>

pkgname=('shorewall' 'shorewall6' 'shorewall-core')
pkgver=4.5.2
pkgrel=2
arch=('any')
url="http://www.shorewall.net/"
license=('GPL')
makedepends=('perl-digest-sha1')
source=(http://www.shorewall.net/pub/$pkgname/4.5/$pkgname-4.5.2/$pkgname-$pkgver.tar.bz2
        http://www.shorewall.net/pub/$pkgname/4.5/$pkgname-4.5.2/shorewall6-$pkgver.tar.bz2
        http://www.shorewall.net/pub/$pkgname/4.5/$pkgname-4.5.2/shorewall-core-$pkgver.tar.bz2)
sha256sums=('4eb6395ae580cb140e94d283d9744c159fe4ffd117410a34df2de141f83f7ae1'
            '1e7d92219088ddcb82dd3c1c1533f4c66e5f138144ecc1bc328b72d7997f686f'
            '35bc7f5368ba8c0d62e73b587cd8a62a61255376f422748817148a9f6918b9eb')

build() {
  for product in shorewall{,6,-core}; do
    pushd "$srcdir/$product-$pkgver"
    ./configure HOST=archlinux SBINDIR=/usr/sbin INITSOURCE=init.archlinux.sh \
      SYSTEMD=/usr/lib/systemd/system
    popd
  done
}

package_shorewall() {
  pkgdesc="An iptables-based firewall for Linux systems"
  depends=('shorewall-core' 'perl-digest-sha1')
  backup=(etc/shorewall/{accounting,actions,blacklist,blrules,clear,ecn,findgw,hosts,init,initdone,interfaces,lib.private,maclist,masq,nat,netmap,notrack,params,policy,providers,proxyarp,refresh,refreshed,restored,routestopped,rtrules,rules,scfilter,secmarks,shorewall.conf,start,started,stop,stopped,tcclasses,tcclear,tcdevices,tcfilters,tcinterfaces,tcpri,tcrules,tos,tunnels,zones})

  cd "$srcdir/$pkgname-$pkgver"

  do_install
}

package_shorewall6() {
  pkgdesc="An iptables-based firewall for Linux systems (with IPv6 support)"
  depends=('shorewall')
  backup=(etc/shorewall6/{accounting,actions,blacklist,blrules,clear,hosts,init,interfaces,maclist,netmap,notrack,params,policy,providers,proxyndp,refresh,refreshed,restored,routestopped,rtrules,rules,scfilter,secmarks,shorewall6.conf,start,started,stop,stopped,tcclasses,tcclear,tcdevices,tcfilters,tcinterfaces,tcpri,tcrules,tos,tunnels,zones})

  cd "$srcdir/$pkgname-$pkgver"

  do_install 6
}

package_shorewall-core() {
  pkgdesc="Core Shorewall libraries"
  depends=('iptables' 'iproute2')

  cd "$srcdir/$pkgname-$pkgver"

  DESTDIR="$pkgdir" ./install.sh
}

do_install() {
  local _name_suffix=$1

  sed -i -e 's|^MODULE_SUFFIX=ko$|&.gz|' \
         -e 's|/subsys||' \
      "configfiles/shorewall$_name_suffix.conf"

  DESTDIR="$pkgdir" ./install.sh

  # Fix /sbin path reference in init scripts
  sed -i 's|/sbin|/usr/sbin|' \
    "$pkgdir/etc/rc.d/shorewall$_name_suffix" \
    "$pkgdir/usr/lib/systemd/system/shorewall$_name_suffix.service"

  install -d "$pkgdir/usr/share/doc/shorewall$_name_suffix/"
  cp -r Samples$_name_suffix "$pkgdir/usr/share/doc/shorewall$_name_suffix/"

  chmod 755 "$pkgdir/etc/rc.d/shorewall$_name_suffix"
  chmod -R 644 "$pkgdir/etc/shorewall$_name_suffix/"
  chmod 755 "$pkgdir/etc/shorewall$_name_suffix/"
  chmod 644 "$pkgdir/usr/share/shorewall$_name_suffix/modules"
  chmod 644 "$pkgdir/usr/share/shorewall$_name_suffix/helpers"

  # Fix systemd service file permissions
  chmod 644 "$pkgdir/usr/lib/systemd/system/shorewall$_name_suffix.service"
}

# vim:set ts=2 sw=2 et:
