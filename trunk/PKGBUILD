# Maintiner: Alad Wenter <alad@archlinux.org>
# Contributor: John Jenkins twodopeshaggy@gmail.com
pkgname=rtv
pkgver=1.24.0
pkgrel=2
pkgdesc="Browse Reddit from your terminal"
arch=('any')
url="https://github.com/michael-lazar/rtv"
license=('custom:MIT')
makedepends=('python-setuptools')
depends=('python-beautifulsoup4' 'python-decorator' 
         'python-kitchen' 'python-six' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('8a792c6112e27876a48db09c18fe3032734066193d6c79614d7df1b83f48e744')

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
