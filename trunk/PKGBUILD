# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>

pkgbase=python-selenium
pkgname=('python-selenium' 'python2-selenium')
pkgver=3.141.0
pkgrel=1
pkgdesc="Python binding for Selenium Remote Control"
arch=('x86_64')
url="https://pypi.python.org/pypi/selenium"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('geckodriver: Firefox driver support')
source=("https://pypi.io/packages/source/s/selenium/selenium-$pkgver.tar.gz")
md5sums=('274693e383ff507df7ee190359828c84')

package_python-selenium() {
  depends=('python' 'python-urllib3')
  cd "$srcdir/selenium-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-selenium() {
  depends=('python2' 'python2-urllib3')
  cd "$srcdir/selenium-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# check() takes too many dependencies to run
