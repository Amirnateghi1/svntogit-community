# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: peter feigl <peter.feigl@gmail.com>

pkgname=mit-scheme
pkgver=10.1.10
pkgrel=3
pkgdesc='MIT/GNU Scheme'
url='https://www.gnu.org/software/mit-scheme/'
arch=('x86_64')
license=('GPL')
depends=('ncurses' 'zlib' 'libx11')
optdepends=('openssl: support for openssl')
provides=('x11-shim.so')
source=("https://ftp.gnu.org/gnu/mit-scheme/stable.pkg/$pkgver/$pkgname-$pkgver-x86-64.tar.gz"
        "https://ftp.gnu.org/gnu/mit-scheme/stable.pkg/$pkgver/$pkgname-$pkgver-x86-64.tar.gz.sig")
validpgpkeys=('424626C56C4AA9DB11194C07E3AA82D81B352C01') # Chris Hanson <cph@chris-hanson.org>
sha256sums=('4f14dd754703736c1843a8c709cd704a24e92b02d6021652d57ae5d6a2d992e6'
            'SKIP')

build() {
    cd "$pkgname-$pkgver"/src
    ./configure --prefix=/usr \
        --with-x \
        --enable-x11 \
        --enable-native-code
    make
    cd ../doc
    ./configure --prefix=/usr \
        --disable-pdf \
        --disable-html
    make
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm644 "etc/xscheme.el" "$pkgdir/usr/share/emacs/site-lisp/xscheme.el"
    cd src
    make DESTDIR="$pkgdir" install
    cd ../doc
    make DESTDIR="$pkgdir" install-info install-man
}
