# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: poisonby <poisonby@tutanota.com>
# Contributor: Jason Scurtu (scujas) <jscurtu@gmail.com>
# Contributor: Marcin Tydelski <marcin.tydelski@gmail.com>
# Contributor: Jan Lukas Gernert (JeanLuc) <https://launchpad.net/~eviltwin1>

pkgname=feedreader
_pkgname=FeedReader
pkgver=2.10.0
pkgrel=2
pkgdesc="Modern desktop application designed to complement existing web-based RSS accounts"
arch=(x86_64)
url="https://jangernert.github.io/FeedReader/"
license=(GPL3)
depends=(curl gnome-keyring gnome-online-accounts gumbo-parser libgee libpeas)
makedepends=(git gobject-introspection meson vala)
_commit=fb074d3e6a5ade2ceb1cdeafbca3cac4a0807c26  # tags/v2.10.0
source=("git+https://github.com/jangernert/$_pkgname#commit=$_commit"
        fix-build.patch)
sha256sums=('SKIP'
            'e0086e131ebc7b30fb3265db0b52f209b9f2e2b8a146f93e6a7d66d5b9676d96')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname
  patch -Np1 -i ../fix-build.patch
}

build() {
  arch-meson $_pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
