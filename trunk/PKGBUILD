# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=pynac
pkgver=0.7.24
pkgrel=2
pkgdesc="Python version of GiNaC, a C++ library for symbolic mathematical calculations"
arch=(x86_64)
url="http://pynac.org"
license=(GPL)
depends=(python2 singular libgiac) # giac disabled because of https://trac.sagemath.org/ticket/26427
source=("https://github.com/pynac/pynac/releases/download/pynac-$pkgver/pynac-$pkgver.tar.bz2")
sha256sums=('219ffdcf6ffaacbd86cc0574cc522b241851ca2a5cb53acd76bf4929aec243e1')

build() {
  cd pynac-$pkgver

  export PYTHON=python2
  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd pynac-$pkgver
  make DESTDIR="$pkgdir" install
}
