# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=FanFicFare
pkgname=fanficfare
pkgver=3.19.0
pkgrel=1
pkgdesc="A tool for downloading fanfiction to eBook formats"
arch=('any')
url="https://github.com/JimmXinu/${_pkgname}"
license=('Apache')
changelog=changelog.txt
depends=('python' 'python-beautifulsoup4' 'python-chardet' 'python-html5lib'
         'python-html2text')
optdepends=('python-pillow: support for converting/resizing story images and covers')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('57132f65181ac18a5040ea76dc1bebe89cea3d55b75d12625be4bec0bcdba65d')
b2sums=('89414f04904128fcd28fe6aa409aab5b492fd192f3ab6f6d851c9efcfe805f3918b52b94429deaeec3b14af5c8f72d8a0daa964aa3f5a078372e5bb021f32e42')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
