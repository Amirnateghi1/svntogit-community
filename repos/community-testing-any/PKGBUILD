# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Dave Pretty <david dot pretty at gmail dot com>

pkgname=anki
pkgver=2.1.8
pkgrel=3

pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently"
url="https://ankisrs.net/"
arch=('any')
license=('AGPL3')

depends=('qt5-webengine' 'python-beautifulsoup4' 'python-pyaudio'
         'python-pyqt5' 'python-pyqtwebengine' 'python-requests' 'python-send2trash'
         'python-decorator' 'python-markdown'
         'desktop-file-utils')
optdepends=('lame: record sound'
            'mpv: play sound (prefered over mplayer)'
            'mplayer: play sound')

source=(https://apps.ankiweb.net/downloads/current/anki-$pkgver-source.tgz
        anki-pyqtwebengine.patch)
sha256sums=('57c466884ba43ef704c438bb0129e7ed793c468d36ea03cd3007af7a394c8b23'
            '56cf512a5f023d61fa2b18348a5586b4489d48ac89a8c60612ef61dbe1809c4a')

prepare() {
  cd anki-$pkgver
  patch -p1 -i ../anki-pyqtwebengine.patch # Compatibility with split pyqtwebengine
}

package() {
  cd anki-$pkgver

  make DESTDIR="$pkgdir" PREFIX=/usr install
}
