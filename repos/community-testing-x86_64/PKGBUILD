# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=libwebrtc
_commit=12f4a27
pkgver=83.git2.${_commit}
pkgrel=1
pkgdesc='WebRTC library - static linked'
arch=('x86_64')
url='https://webrtc.org/?hl=de'
license=('custom:BSD')
depends=('protobuf')
makedepends=('git' 'ninja' 'unzip' 'cmake' 'libxrandr' 'libxcomposite' 'openssl' 'glibc' 'ffmpeg' 'libva' 'opus' 'yasm' 'libjpeg-turbo')
options=('staticlibs')
source=("tg_owt::git+https://github.com/desktop-app/tg_owt.git#commit=${_commit}")
b2sums=('SKIP')


build() {
  cd tg_owt
  mkdir build
  cd build
  # path to openssl include is intentionally wrong, so that it will not mess up srtp include headers
  cmake -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DTG_OWT_SPECIAL_TARGET=linux \
    -DTG_OWT_LIBJPEG_INCLUDE_PATH=/usr/include \
    -DTG_OWT_OPENSSL_INCLUDE_PATH=/usr/include \
    -DTG_OWT_OPUS_INCLUDE_PATH=/usr/include/opus \
    -DTG_OWT_FFMPEG_INCLUDE_PATH=/usr/include \
    ..
  #  -DTG_OWT_LIBJPEG_INCLUDE_PATH=$(pwd)/../../mozjpeg/output/include \
  ninja
}

package() {
  cd tg_owt/build
  DESTDIR="${pkgdir}/" ninja install
}
