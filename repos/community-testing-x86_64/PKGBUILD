# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface
_pkgname=Subsurface
pkgver=4.7.8
pkgrel=3
pkgdesc='Divelog program'
url='https://subsurface-divelog.org/'
license=('GPL2')
arch=('x86_64')
makedepends=('cmake' 'asciidoc' 'qt5-tools')
depends=('libzip' 'libxml2' 'libxslt' 'sqlite' 'libusb' 'libgit2'
         'subsurface-libdc' 'qt5-svg' 'qt5-location'
         'qt5-connectivity' 'qt5-webkit' 'grantlee' 'googlemaps')
source=("https://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz"
        'include.patch'
        'delete.patch')
sha256sums=('da7c09bcc63e0b3bc6e1ce5411a4f1e38a73e6e37bce15055657d840c78b07fa'
            '05d8ed15a42b691043617f9a5e53047db49ab7dc288778300f8c433c644e70b1'
            '59135a6917643be1608eb88db1dc81a740c8b0cff01f9ed62fa80e7d7523ee47')

# qt5-webkit still used for: printing, manual, facebook

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	patch -p1 -i ../include.patch
	patch -p1 -i ../delete.patch
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	install -d build
	cd build
	cmake \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DLIBDIVECOMPUTER_INCLUDE_DIR=/usr/include/libdivecomputer \
		..
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	cd build
	make DESTDIR="${pkgdir}" install
}
