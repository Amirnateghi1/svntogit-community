# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.9.12
pkgrel=3
pkgdesc="Creates DjVu files from PDF files"
arch=('x86_64')
url="https://jwilk.net/software/pdf2djvu"
license=('GPL')
depends=('poppler' 'djvulibre' 'gcc-libs' 'graphicsmagick' 'exiv2')
makedepends=('python2-nose' 'ttf-liberation')
source=(https://github.com/jwilk/pdf2djvu/releases/download/$pkgver/pdf2djvu-$pkgver.tar.xz{,.asc}
        pdf2djvu-poppler-0.72.0.patch::https://github.com/jwilk/pdf2djvu/commit/82699eee6b.patch)
sha256sums=('03fa9d9aa1b8086b562e2461ed74e5f9f008f16bfc1459ba62fa3738b37d8e83'
            'SKIP'
            'cc5ef76568fdd201586fa6e0b8e8e386628972ad75a597ea0790bb97718eef87')
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}

    make PYTHON=python2 test
}

package() {
    cd ${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
}
