# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=pynac
pkgver=0.7.3
pkgrel=2
pkgdesc="Python version of GiNaC, a C++ library for symbolic mathematical calculations"
arch=(i686 x86_64)
url="http://pynac.org"
license=(GPL)
depends=(python2 libgiac singular)
source=("https://github.com/pynac/pynac/releases/download/pynac-$pkgver/pynac-$pkgver.tar.bz2"
        pynac-0.7.3-fix-linking.patch::"https://github.com/pynac/pynac/commit/23f41c28.patch")
md5sums=('c76c749bc4301d09b90073bcaa230a24'
         '8082fcb68661cb43b99545c3872d645e')

prepare() {
  cd pynac-$pkgver
# Fix underlinking with giac enabled
  patch -p1 -i ../pynac-0.7.3-fix-linking.patch
}

build() {
  cd pynac-$pkgver

  export PYTHON=python2
  ./configure --prefix=/usr
  make
}

package() {
  cd pynac-$pkgver
  make DESTDIR="$pkgdir" install
}
