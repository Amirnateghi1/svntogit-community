# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: wahnby <wahnby@yahoo.fr>

pkgname=gnunet
pkgver=0.9.1
pkgrel=1
pkgdesc="A framework for secure peer-to-peer networking"
arch=('i686' 'x86_64')
url="http://gnunet.org"
license=('GPL')
depends=('gmp' 'libgcrypt' 'libextractor' 'sqlite3' 'curl' 'libmicrohttpd')
makedepends=('gettext' 'pkgconfig')
backup=(etc/gnunetd.conf)
options=('!libtool' '!makeflags')
install=gnunet.install
source=(ftp://ftp.gnu.org/gnu/gnunet/gnunet-$pkgver.tar.gz
	gnunet.rc
	gnunet.conf.d
	defaults.conf)
md5sums=('f5ff4c6a87e7b24047319af46113e5c3'
         '5baf691913b007fb33e365239e3f7217'
         'f161b46915736e4017e77ad735521453'
         '0fe23b2ca5b3fc47a0b5645e04406da0')

build() {
  cd $srcdir/gnunet-$pkgver
  [ -f Makefile ] || ./configure --prefix=/usr --without-mysql
  make
}

package() {
  cd $srcdir/gnunet-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m0755 $srcdir/gnunet.rc $pkgdir/etc/rc.d/gnunetd
  install -D -m0644 $srcdir/gnunet.conf.d $pkgdir/etc/conf.d/gnunetd
  install -D -m0644 $srcdir/defaults.conf $pkgdir/etc/gnunetd.conf
  rm -rf $pkgdir/usr/include/libltdl $pkgdir/usr/lib/libltdl.* $pkgdir/usr/include/ltdl.h
}
