# $Id$
# Maintainer: Vesa Kaihlavirta
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Patrick Smits <mail@patricksmits.net>

pkgname=yaws
pkgver=1.95
pkgrel=4
pkgdesc='Yet Another Web Server, pure Erlang HTTP server/framework'
arch=('x86_64' 'i686')
url='http://yaws.hyber.org/'
license=('BSD')
depends=('pam' 'erlang')
backup=('etc/yaws/yaws.conf')
options=('!emptydirs')
install=$pkgname.install
optdepends=('erlang-nox: erlang without X')
source=("http://yaws.hyber.org/download/$pkgname-$pkgver.tar.gz"
        "$pkgname.service")
sha256sums=('e4efae836561f5e8a1324a5c9ab63fb82468e9dc93dc95cb4cb6248ab6b95406'
            '3f8347add6e3741e77f7bb9ba6b31feb1c878e2e58ecf19063891e801fa24698')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  # python2 fix
  for file in cgi.yaws cgi-bin/foo.py; do
	  sed -i '0,/on/s//on2/' "www/$file"
  done
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/lib/erlang/lib"
  ln -s /usr/lib/yaws "$pkgdir/usr/lib/erlang/lib/$pkgname-$pkgver"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 et:
