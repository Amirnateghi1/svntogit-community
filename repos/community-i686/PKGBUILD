# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=pynac
pkgver=0.7.9
pkgrel=2
pkgdesc="Python version of GiNaC, a C++ library for symbolic mathematical calculations"
arch=(i686 x86_64)
url="http://pynac.org"
license=(GPL)
depends=(python2 singular)
source=("https://github.com/pynac/pynac/releases/download/pynac-$pkgver/pynac-$pkgver.tar.bz2"
        pynac-fix-build.patch::"https://github.com/pynac/pynac/commit/e234ea37.patch")
sha256sums=('b0c32f610153ffa8ff5baf312294843d6b51f4f23b56332050d4ca3b97f2becd'
            '35ff1d82c94c92e9ec2f5c010ca7852a5b16400f3735bc87d8b4e4ed746a40ce')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../pynac-fix-build.patch
}

build() {
  cd pynac-$pkgver

  export PYTHON=python2
  ./configure --prefix=/usr
  make
}

package() {
  cd pynac-$pkgver
  make DESTDIR="$pkgdir" install
}
