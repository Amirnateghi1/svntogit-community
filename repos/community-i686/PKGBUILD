# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=moneymanagerex
pkgver=1.3.1
pkgrel=1
pkgdesc="An easy-to-use personal finance suite"
arch=('i686' 'x86_64')
url="http://www.moneymanagerex.org/"
license=('GPL')
depends=('wxgtk' 'webkitgtk2' 'hicolor-icon-theme')
makedepends=('git' 'boost' 'gettext')
optdepends=('cups: for printing support')
replaces=('mmex')
_commit=('52159cdb7362ba9b4714c64ff0b2d988c6256d6f')
source=(git+https://github.com/moneymanagerex/moneymanagerex.git#commit=${_commit})
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}"/$pkgname

  git submodule update --init
}

build() {
  cd "${srcdir}"/$pkgname

  ./configure --prefix=/usr --disable-shared --enable-unicode
  make
  make po
}

package() {
  cd "${srcdir}"/$pkgname

  make DESTDIR="${pkgdir}" install
}
