# Maintainer: schuay <jakob.gruber@gmail.com>
# Previous Maintainer:  TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo AT gmail DOT com>

pkgname=openssh-askpass
pkgver=1.3.1
pkgrel=2
pkgdesc='A plasma-like passphrase dialog for ssh'
arch=('i686' 'x86_64')
url='http://www.phenix.bnl.gov/WWW/publish/hpereira/software/index.php?page=package&package_list=software_list_qt4&package=openssh-askpass&full=1'
license=('GPL')
depends=('openssh' 'qt')
provides=('x11-ssh-askpass')
conflicts=('x11-ssh-askpass')
source=("http://www.phenix.bnl.gov/WWW/publish/hpereira/software/tgz/$pkgname-$pkgver.tar.gz"
        "$pkgname.sh")
md5sums=('39178e7ef71a1846c0a464cef08ba38e'
         '961738244318f3723ba99cdcaac91a21')
         
build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh --prefix=/usr --with-qt-dir=/usr --bindir=/usr/lib/openssh
  make
}
package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm755 ../$pkgname.sh "$pkgdir/etc/profile.d/$pkgname.sh"
}
