# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=libmemcached
pkgver=1.0.8
pkgrel=1
pkgdesc="C and C++ client library to the memcached server"
arch=('i686' 'x86_64')
url="http://libmemcached.org/"
license=('GPL')
depends=('glibc' 'libsasl' 'libevent')
makedepends=('perl')
options=('!libtool')
source=(http://launchpad.net/$pkgname/1.0/$pkgver/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('03f4eb91634f1434a93d4709eccd0c4cce3eb296b54f2b1998ca4a9ce1661ca5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # memcached is needed only in `make test', therefore we pass
  # --without-memcached to the configure script to skip it
  ./configure --prefix=/usr --without-memcached
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
