# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Daniel Hommel <dhommel@gmail.com>
pkgname=mksh
pkgver=R40c
pkgrel=1
pkgdesc="The MirOS KornShell - an enhanced version of the public domain ksh"
url="http://mirbsd.de/mksh"
license=("custom")
makedepends=('cpio')
arch=('i686' 'x86_64')
install=mksh.install
source=("https://www.mirbsd.org/MirOS/dist/mir/mksh/$pkgname-$pkgver.cpio.gz"
        "copyright")
md5sums=('43a79f721091833bdab3d00fbfe54a14'
         '6c548c9ef504d590358e30cb6db25bc1')

build() {
  cd "$srcdir"
  # makepkg can't extract cpio files
  cpio -mid < "$pkgname-$pkgver.cpio"

  cd "$srcdir/$pkgname"
  sh Build.sh -r -c lto
  ./test.sh
  gzip mksh.1
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m 755 mksh "$pkgdir/bin/mksh"
  install -D -m 644 mksh.1.gz "$pkgdir/usr/share/man/man1/mksh.1.gz"
  install -D -m 644 dot.mkshrc "$pkgdir/etc/skel/.mkshrc"
  install -D -m 644 "$srcdir/copyright" "$pkgdir/usr/share/licenses/mksh/copyright"
}

# vim:set ts=2 sw=2 et:
