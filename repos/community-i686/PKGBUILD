# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lizao (Larry) Li <lzlarryli@gmail.com>

pkgname=libcurl-gnutls
pkgver=7.52.1
pkgrel=1
pkgdesc='An URL retrieval library'
arch=('i686' 'x86_64')
url='https://curl.haxx.se'
license=('MIT')
depends=('curl' 'glibc' 'gnutls' 'libpsl' 'nettle' 'zlib'
         'libssh2.so')
options=('strip')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc}
        '0001-vtls-s-SSLEAY-OPENSSL.patch')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg
sha256sums=('a8984e8b20880b621f61a62d95ff3c0763a3152093a9f9ce4287cfd614add6ae'
            'SKIP'
            'a850c761e5fd1c312328a0168c9ce2eefbf9a96f960c5db9e6e4812693379b49')

prepare() {
  cd curl-${pkgver}

  patch -Np1 -i ../0001-vtls-s-SSLEAY-OPENSSL.patch
}

build() {
  cd curl-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --disable-versioned-symbols \
    --enable-ipv6 \
    --enable-threaded-resolver \
    --without-gssapi \
    --without-libidn \
    --without-ssl \
    --with-gnutls='/usr' \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'

  make -C lib
}

package() {
  cd curl-${pkgver}

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib/libcurl{,-gnutls}.so.4.4.0
  rm "${pkgdir}"/usr/lib/libcurl.{a,so}*
  for version in 3 4 4.0.0 4.1.0 4.2.0 4.3.0; do
    ln -s libcurl-gnutls.so.4.4.0 "${pkgdir}"/usr/lib/libcurl-gnutls.so.${version}
  done

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s curl "${pkgdir}"/usr/share/licenses/libcurl-gnutls
}

# vim: ts=2 sw=2 et:
