# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=liboping
pkgver=1.6.1
pkgrel=1
pkgdesc='C library to generate ICMP echo requests, better known as "ping packets"'
url='http://verplant.org/liboping/'
arch=('i686' 'x86_64')
license=('GPL')
options=('!libtool')
depends=('libcap')
makedepends=('ncurses')
optdepends=('perl: perl bindings'
            'ncurses: noping CLI tool')
source=("${url}files/$pkgname-$pkgver.tar.gz")
sha1sums=('8203096fa932d157069be7aeda3a1025132e129f')

install=install

build() {
	cd "$srcdir/$pkgname-$pkgver"

	# setting capabilities/setuid is futile in fakeroot
	# (we do that in the install script)
	sed -i 's/ install-exec-hook//g' src/Makefile.in

	./configure --prefix=/usr --enable-static=no
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}
