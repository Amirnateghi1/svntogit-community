# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-phone-manager
pkgver=0.68
pkgrel=3
pkgdesc="Control your mobile phone from your GNOME desktop."
arch=('i686' 'x86_64')
url="http://live.gnome.org/PhoneManager"
license=('GPL')
depends=('evolution-data-server' 'gnokii' 'gnome-bluetooth' 'gnome-icon-theme' 'libcanberra' 'gconf')
makedepends=('intltool')
options=('!emptydirs' '!libtool')
install=gnome-phone-manager.install
source=(ftp://ftp.archlinux.org/other/gnome-phone-manager/gnome-phone-manager-0.68-git20121019.tar.xz
        gnome-phone-manager-0.68-eds.patch)
sha256sums=('885dfa80989c633a5ae8b118c9b7c903e18e44f0e9fc89e6a9bae05da87c9b15'
            '98d5c2d3034081eebbaa0dac85f53dcc40c8970114f5711aff8c37bab2931a66')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../gnome-phone-manager-0.68-eds.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-phone-manager "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
