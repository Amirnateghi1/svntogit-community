From 8f8abd65767ad5d1f2b5d675509789d6c04524d9 Mon Sep 17 00:00:00 2001
From: Jakub Wilk <jwilk@jwilk.net>
Date: Fri, 11 Nov 2016 17:26:13 +0100
Subject: [PATCH] configure: enable the C++11 standard.

---
 acinclude.m4 | 57 ++++++++++++++++++++++++++++++++++++++++++++++++++++++++-
 configure.ac |  3 ++-
 2 files changed, 58 insertions(+), 2 deletions(-)

diff --git a/acinclude.m4 b/acinclude.m4
index 4dedbfc..c12339c 100644
--- a/acinclude.m4
+++ b/acinclude.m4
@@ -1,4 +1,4 @@
-dnl | Copyright © 2015 Jakub Wilk <jwilk@jwilk.net>
+dnl | Copyright © 2015-2016 Jakub Wilk <jwilk@jwilk.net>
 dnl |
 dnl | This file is part of pdf2djvu.
 dnl |
@@ -95,4 +95,59 @@ AC_DEFUN(
     ]
 )
 
+# AX_CXX_COMPILE_STDCXX_11 is too thorough. It would reject GCC 4.6, even
+# though this compiler implements enough C++11 bits for our purposes.
+
+m4_define([_P_CXX11_CODE], [
+    void* test()
+    {
+        auto t = nullptr;
+        return t;
+    }
+])
+
+# P_CXX11()
+
+AC_DEFUN(
+    [P_CXX11],
+    [
+        have_cxx11=no
+        AC_MSG_CHECKING([whether $CXX supports C++11])
+        AC_COMPILE_IFELSE(
+            [AC_LANG_PROGRAM([_P_CXX11_CODE])],
+            [
+                AC_MSG_RESULT([yes])
+                have_cxx11=yes
+            ],
+            [
+                AC_MSG_RESULT([no])
+                for cxx_std in 'gnu++11' 'gnu++0x'
+                do
+                    cxx_opt="-std=$cxx_std"
+                    AC_MSG_CHECKING([whether $CXX $cxx_opt supports C++11])
+                    p_CXXFLAGS="$CXXFLAGS"
+                    CXXFLAGS="$CXXFLAGS $cxx_opt"
+                    AC_COMPILE_IFELSE(
+                        [AC_LANG_PROGRAM([_P_CXX11_CODE])],
+                        [
+                            AC_MSG_RESULT([yes])
+                            have_cxx11=yes
+                        ],
+                        [
+                            AC_MSG_RESULT([no])
+                            CXXFLAGS="$p_CXXFLAGS"
+                        ]
+                    )
+                    test $have_cxx11 = yes && break
+                done
+            ]
+        )
+        if test $have_cxx11 = no
+        then
+            AC_MSG_ERROR([the compiler does not support C++11])
+        fi
+    ]
+)
+
+
 dnl vim:ts=4 sts=4 sw=4 et ft=config
diff --git a/configure.ac b/configure.ac
index 38eec41..67b8c9e 100644
--- a/configure.ac
+++ b/configure.ac
@@ -30,6 +30,8 @@ AC_PREPROC_IFELSE(
 
 AC_PROG_CXX
 
+P_CXX11
+
 AC_SYS_LARGEFILE
 
 AC_OPENMP
@@ -165,7 +167,6 @@ P_CHECK_FUNC(
 # Turn on compile warnings:
 
 P_MAYBE_ADD_CXXFLAGS(
-  [-std=gnu++98],
   [-Wall],
   [-Wempty-body],
   [-Werror=overloaded-virtual],
-- 
2.13.3

