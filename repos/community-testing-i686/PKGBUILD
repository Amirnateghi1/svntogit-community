# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Gerhard Brauer <gerhard.brauer@web.de>

pkgname=collectd
pkgver=5.0.0
pkgrel=6
pkgdesc='Daemon which collects system performance statistics periodically'
arch=('i686' 'x86_64')
url='http://collectd.org/'
license=('GPL')
options=('!libtool')
backup=('etc/collectd.conf')

optdepends=('curl: apache, ascent, curl, nginx, and write_http plugins'
            'libdbi: dbi plugin'
            'libesmtp: notify_email plugin'
            'libgcrypt: encryption and authentication for network plugin'
            'iptables: iptables plugin'
            'libmemcached: memcachec plugin'
            'libmysqlclient: mysql plugin'
            'iproute2: netlink plugin'
            'net-snmp: snmp plugin'
            'libnotify: notify_desktop plugin'
            'liboping: ping plugin'
            'libpcap: dns plugin'
            'perl: perl plugin'
            'postgresql-libs: postgresql plugin'
            'python2: python plugin'
            'rrdtool: rrdtool and rrdcached plugins'
            'lm_sensors: lm_sensors and sensors plugins'
            'libvirt: libvirt plugin'
            'libxml2: ascent and libvirt plugins'
            'xmms: xmms plugin'
            'yajl: curl_json plugin')

makedepends=('curl' 'libdbi' 'libesmtp' 'libgcrypt' 'iptables' 'libmemcached'
             'libmysqlclient' 'iproute2' 'net-snmp' 'libnotify' 'liboping'
             'libpcap' 'postgresql-libs' 'python2' 'rrdtool' 'lm_sensors'
             'libvirt' 'libxml2' 'xmms' 'yajl')

depends=('libtool')

source=("${url}files/$pkgname-$pkgver.tar.gz"
        'libnotify-0.7.patch'
        'yajl-2.x.patch'
        'rc.d')
sha1sums=('026e5121348f0e525dedb3844fe61c7713994bb7'
          'f6fed097c16f6c9c90b9a32a5b8e48d54b35b337'
          'd3854c39c9596b4f6dcf67a9eb8decea4d17120d'
          '0f441718d5519cb043b1130e5a1d0379078adbcc')

install=install

build() {
	cd "$srcdir/$pkgname-$pkgver"

	unset LDFLAGS # or libperl not found
	patch -p1 -i ../libnotify-0.7.patch
	patch -p1 -i ../yajl-2.x.patch
	sed -i 's/ipt_error_target/bozo_error_target/g' src/owniptc/libiptc.c

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-static=no \
		--with-python=/usr/bin/python2

	# avoid -Werror with gcc 4.6
	sed -i '/^AM_CFLAGS/s/ -Werror//' src/Makefile

	make all
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install

	install -D -m 755 ../rc.d "$pkgdir/etc/rc.d/$pkgname"
	install -D -m 644 contrib/collectd2html.pl "$pkgdir"/usr/share/collectd/collectd2html.pl
}
