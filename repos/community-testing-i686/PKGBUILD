# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sylpheed
pkgver=3.5.1
pkgrel=2
pkgdesc="Lightweight and user-friendly e-mail client"
arch=('i686' 'x86_64')
url="http://sylpheed.sraoss.jp/en/"
license=('GPL')
depends=('compface' 'gpgme' 'gtkspell')
makedepends=('openssl')
source=("http://sylpheed.sraoss.jp/$pkgname/v${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
md5sums=('d88e892b54b160209c8183ebe2574490')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-ldap
  make

  # Build Attachment-Tool Plug-in
  cd plugin/attachment_tool && make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # Install Attachment-Tool Plug-in
  cd plugin/attachment_tool
  make DESTDIR="$pkgdir/" install-plugin
}
