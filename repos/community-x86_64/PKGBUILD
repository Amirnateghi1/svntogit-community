# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=Kvantum
pkgname=kvantum-qt5
pkgver=0.10.7
pkgrel=1
pkgdesc="SVG-based theme engine for Qt5 (including config tool and extra themes)"
arch=('x86_64')
url="https://github.com/tsujan/Kvantum"
license=('GPL')
depends=('qt5-base' 'qt5-svg' 'qt5-x11extras' 'libx11' 'libxext' 'hicolor-icon-theme')
makedepends=('cmake' 'qt5-tools')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/V${pkgver}.tar.gz")
sha256sums=('dd7703be481907012823470f703e462db7339ad1ebd9b30c1848b7c6998b82d6')

prepare() {
    mkdir -p build
}

build() {
    cd build
	cmake ../${_pkgname}-${pkgver}/${_pkgname} \
	      -DCMAKE_INSTALL_PREFIX:PATH=/usr
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
