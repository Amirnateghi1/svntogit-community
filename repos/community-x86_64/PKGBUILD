# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>

pkgbase=python-selenium
pkgname=('python-selenium' 'python2-selenium')
pkgver=3.4.0
pkgrel=1
pkgdesc="Python binding for Selenium Remote Control"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/selenium"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('geckodriver: Firefox driver support')
source=('https://pypi.python.org/packages/e8/42/3cbe4cbde83a0bf6470c456301f2140d0b315e720660564919766e778e58/selenium-3.4.0.tar.gz')
md5sums=('3f869785a289bd42df1c4d1f65f88df8')

package_python-selenium() {
  depends=('python')
  cd "$srcdir/selenium-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-selenium() {
  depends=('python2')
  cd "$srcdir/selenium-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# check() takes too many dependencies to run
