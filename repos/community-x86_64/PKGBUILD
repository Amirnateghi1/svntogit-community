# Maintainer: David Runge <dave@sleepmap.de>
pkgname=giada
pkgver=0.14.6
pkgrel=3
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=('x86_64')
url="https://www.giadamusic.com/"
license=('GPL3')
groups=('pro-audio')
depends=('fltk' 'jansson' 'libpulse' 'libxpm' 'rtmidi')
makedepends=('gendesk')
source=("$pkgname-$pkgver.tar.gz::https://github.com/monocasual/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('e3d5e0c06acbd0f46f82bf51ff5de0ef318a4d3a2ad2a5779b626397988c55cbadce847637b49e9c3d734fdaae6bf94ab1366038f1992be0c22a7daf83e39390')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Giada \
          --categories "MIDI;Sequencer"
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --target=linux
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # XDG integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "src/ext/${pkgname}.ico" -t "${pkgdir}/usr/share/pixmaps"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
