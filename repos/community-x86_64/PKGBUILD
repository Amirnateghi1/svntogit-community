# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Alexey Yakovenko <waker@users.sourceforge.net>

pkgname=deadbeef
pkgver=1.8.3
pkgrel=1
pkgdesc='A GTK+ audio player for GNU/Linux.'
arch=('x86_64')
url='http://deadbeef.sourceforge.net'
license=('GPL2')
depends=('alsa-lib' 'hicolor-icon-theme' 'desktop-file-utils' 'jansson')
makedepends=('libvorbis' 'libmad' 'flac' 'curl' 'imlib2' 'wavpack' 'libsndfile' 'libcdio' 'libcddb'
             'libx11' 'faad2' 'zlib' 'intltool' 'pkgconfig' 'libpulse' 'libzip' 'libsamplerate'
             'yasm' 'ffmpeg' 'gtk2' 'gtk3')
optdepends=('gtk2: for the GTK2 interface'
            'gtk3: for the GTK3 interface'
            'libsamplerate: for Resampler plugin'
            'libvorbis: for Ogg Vorbis playback'
            'libmad: for MP1/MP2/MP3 playback'
            'mpg123: for MP1/MP2/MP3 playback'
            'flac: for FLAC playback'
            'curl: for Last.fm scrobbler, SHOUTcast, Icecast, Podcast support'
            'imlib2: for artwork plugin'
            'wavpack: for WavPack playback'
            'libsndfile: for Wave playback'
            'libcdio: audio cd plugin'
            'libcddb: audio cd plugin'
            'faad2: for AAC/MP4 support'
            'dbus: for OSD notifications support'
            'pulseaudio: for PulseAudio output plugin'
            'libx11: for global hotkeys plugin'
            'zlib: for Audio Overload plugin'
            'libzip: for vfs_zip plugin'
            'ffmpeg: for ffmpeg plugin')
source=("https://github.com/DeaDBeeF-Player/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('d0101b730fc8504b990929fa255d88010c544c1d2090ec12c79106a2b1dbb9613d03f06f8a872ec7c2d61faab4c23993b7a7fcb4b3b8dbc91dc4a065a1c459a3')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	./autogen.sh 
	./configure --prefix=/usr
}

build () {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir" install
}
