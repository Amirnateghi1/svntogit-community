# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface-libdc
_pkgname=libdivecomputer-subsurface-branch
pkgver=4.8.4
pkgrel=1
pkgdesc='Library for communication with dive computers'
url='https://github.com/Subsurface-divelog/libdc'
license=('LGPL')
arch=('x86_64')
source=("https://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz")
sha256sums=('a23a92d25e4f1c4d079647154bf0b93482640b34310ea40844ac4cbc86a98d4c')

conflicts=('libdivecomputer')
provides=('libdivecomputer')
replaces=('libdivecomputer')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	autoreconf --install
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
