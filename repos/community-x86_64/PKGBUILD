# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=giada
pkgver=0.16.1
pkgrel=1
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=('x86_64')
url="https://www.giadamusic.com/"
license=('GPL3')
groups=('pro-audio')
depends=('alsa-lib' 'fltk' 'gcc-libs' 'glibc' 'jansson' 'libjack.so' 'libpulse'
'librtmidi.so' 'libsamplerate' 'libsndfile' 'libx11' 'libxpm')
makedepends=('gendesk' 'imagemagick')
checkdepends=('catch2' 'xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/monocasual/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('12a898d449bea7d189eb320ea1241c0e408eafbf2460ff122b1383aff4209b5af4513ba5a2d4684cbbcaaf5a866d8016f19337497c7cd4faf58b884663543c35')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Giada \
          --categories "AudioVideo;Audio;Midi;Sequencer"
  # fixing catch2 include for system library
  sed -e 's|catch\.hpp|catch2/catch\.hpp|g' -i tests/*.cpp
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --target=linux \
              --enable-system-catch
  make
}

check(){
  cd "$pkgname-$pkgver"
  xvfb-run -a make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # XDG integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "extras/${pkgname}-logo.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
