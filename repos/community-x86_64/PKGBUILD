# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libjamiclient
pkgver=20190813
pkgrel=1
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (client communication library)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(jami-daemon qt5-base)
makedepends=(git cmake qt5-tools)
provides=(libringclient)
conflicts=(libringclient)
replaces=(libringclient)
_commit=d67fad1c7b89102727a20bc3793e6586b53e95c1
source=("git+https://git.jami.net/savoirfairelinux/ring-lrc.git#commit=${_commit}")
md5sums=(SKIP)

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ring-lrc \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
