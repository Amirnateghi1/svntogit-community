# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>

pkgbase=python-selenium
pkgname=('python-selenium' 'python2-selenium')
pkgver=3.0.1
pkgrel=1
pkgdesc="Python binding for Selenium Remote Control"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/selenium"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('geckodriver: Firefox driver support')
source=("https://pypi.python.org/packages/3a/a3/e4ab60a0229a85f468a36367bc0672a4bca2720f24391eda33704a5f0ad5/selenium-3.0.1.tar.gz")
md5sums=('ed5da4a35e1e643a53386d3e8f5960af')

package_python-selenium() {
  depends=('python')
  cd "$srcdir/selenium-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-selenium() {
  depends=('python2')
  cd "$srcdir/selenium-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# check() takes too many dependencies to run
