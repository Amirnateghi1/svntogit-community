# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-scandir
pkgver=1.8
pkgrel=1
pkgdesc="A better directory iterator and faster os.walk()"
arch=('x86_64')
url="https://github.com/benhoyt/scandir"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/benhoyt/scandir/archive/v$pkgver.tar.gz")
sha512sums=('d0d1594fe2898124e7441ad56a84cecaa6d96409bbdf356c552472086626bee837eb6ad11f3d2a14bfb7a77bac3b5c90561e1f40ce9dcfc7d7d09a531315aa73')

build() {
  cd scandir-$pkgver
  python2 setup.py build
}

check() {
  cd scandir-$pkgver
  LC_CTYPE=en_US.UTF-8 python2 test/run_tests.py
}

package() {
  cd scandir-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
