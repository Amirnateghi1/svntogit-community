# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=subsurface
_pkgname=Subsurface
pkgver=4.9.9
pkgrel=1
pkgdesc='Divelog program'
url='https://subsurface-divelog.org/'
license=('GPL2')
arch=('x86_64')
makedepends=('asciidoc' 'cmake' 'libgit2' 'libusb' 'qt5-tools')
depends=('googlemaps' 'grantlee' 'hicolor-icon-theme' 'libzip' 'libxml2'
         'libxslt' 'subsurface-libdc' 'qt5-svg' 'qt5-location' 'qt5-connectivity'
         'qt5-webkit' 'sqlite')
source=("https://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz")
sha512sums=('8e00b61dad580349bef615c0eaf735db1dd5a5ed8b309fe33367598034f5b22fd376a7ee9fcce8c2a2947326eafb010e5cec5bdc4ef0f8dc03d45f8f6bc3e27c')
b2sums=('e33a00aed92a0751ff4d3d09dd42ce6d0e2eed0dab57f6cca6da4840468b1a43367cf3e14226f64cdac89015a88866b71cee73df0185185feded32d03da73f2c')

# qt5-webkit still used for: printing, manual

build() {
  cmake -B build -S Subsurface-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIBDIVECOMPUTER_INCLUDE_DIR=/usr/include/libdivecomputer \
        -Wno-dev
  cmake --build build
}

package() {
  depends+=('libgit2.so')
  DESTDIR="${pkgdir}" cmake --install build
}
