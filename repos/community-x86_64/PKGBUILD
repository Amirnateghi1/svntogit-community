# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal-old
pkgver=5.0.4.2
pkgrel=1
pkgdesc='Default terminal emulation application for Deepin, the old version'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-terminal-old"
license=('GPL3')
depends=('vte3' 'libsecret' 'libgee' 'libwnck3' 'expect')
makedepends=('cmake' 'ninja' 'vala')
optdepends=('deepin-menu: for native menu integration under DDE'
            'zssh: for remote management support')
conflicts=('deepin-terminal')
provides=('deepin-terminal')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-terminal-old/archive/$pkgver.tar.gz")
sha512sums=('3cf80f56b74412228a4835a1a219f0fd8ac33e067c32f170ca8c090bfd15d86a4635cfc290370d77938f91993932fbf833d83a0d616aa0cf1648a8c2a2f8114a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DTEST_BUILD=OFF \
    -DUSE_VENDOR_LIB=OFF \
    -DVERSION=$pkgver \
    ../deepin-terminal-old-$pkgver
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
  ln -s deepin-terminal "$pkgdir"/usr/bin/x-terminal-emulator
}
