# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# Contributor: Teo Mrnjavac <teo@kde.org>

pkgname=qtkeychain
pkgver=0.6
pkgrel=1
pkgdesc='Provides support for secure credentials storage'
arch=(i686 x86_64)
url='https://github.com/frankosterfeld/qtkeychain'
license=(BSD)
depends=(qt5-base)
conflicts=(qtkeychain-qt5)
provides=(qtkeychain-qt5)
replaces=(qtkeuchain-qt5)
makedepends=(cmake qt5-tools)
optdepends=('kwallet: kwallet support' 'libgnome-keyring: GNOME keyring support')
source=("$pkgname-$pkgver.zip::https://github.com/frankosterfeld/qtkeychain/archive/$pkgver.zip")
md5sums=('44eb27f44708532f9ec222c1d2e2fb85')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../$pkgname-$pkgver \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
          -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
    install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
