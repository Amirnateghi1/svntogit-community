# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=zanshin
pkgver=0.5.0+179+g074d2c34
pkgrel=1
_commit=074d2c34883a7136ce07138dd0de62a370936f9e
pkgdesc="To-do management application based on Akonadi"
arch=(x86_64)
url="https://zanshin.kde.org"
license=(GPL)
depends=(kdepim-runtime kontactinterface)
makedepends=(extra-cmake-modules kdoctools boost krunner kdesignerplugin kdelibs4support git)
#source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
source=(git://git.kde.org/zanshin.git#commit=$_commit)
sha256sums=('SKIP')
validpgpkeys=(17166A5275C5FA6F1A48FD78074BBBCB8DECC9E2) # Kevin Ottens <ervin+bluesystems@kde.org>

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/-/+/g' -e 's/v//'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
