# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=gnurl
pkgver=7.65.0
pkgrel=1
pkgdesc="fork of libcurl, which is mostly for GNUnet"
arch=(x86_64)
url="https://gnunet.org/gnurl"
license=('MIT')
depends=('gnutls' 'krb5' 'libnghttp2')
source=("https://ftp.gnu.org/gnu/gnunet/gnurl-$pkgver.tar.Z"{,.sig})
validpgpkeys=('A88C8ADD129828D7EAC02E52E22F9BBFEE348588')  # Nils Gillmann
sha512sums=('21c945a177f60de1d92e9028070904cfba29a0efdfbef4afc0450d5236e3bcfb0e82f4ec638aa1f3427ab1a8fdb77c8358d5c44d3c8787b310d2a7eeb69baa41'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  ./buildconf
  ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-dict \
        --disable-file \
        --disable-ftp \
        --disable-gopher \
        --disable-imap \
        --disable-ldap \
        --disable-ldaps \
        --disable-ntlm-wb \
        --disable-pop3 \
        --disable-rtsp \
        --disable-smb \
        --disable-smtp \
        --disable-telnet \
        --disable-tftp \
        --enable-ipv6 \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --without-gssapi \
        --with-gnutls \
        --without-libidn \
        --without-libpsl \
        --without-librtmp \
        --without-ssl \
        --with-random=/dev/urandom \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

#  rm -rf "$pkgdir"/usr/share/man \
#    "$pkgdir"/usr/bin/curl \
#    "$pkgdir"/usr/share/aclocal
#  mkdir -p "$pkgdir"/usr/include/gnurl
#  mv "$pkgdir"/usr/include/curl "$pkgdir"/usr/include/gnurl/curl
#  sed -i 's|includedir=.*|includedir=/usr/include/gnurl|g' "$pkgdir"/usr/lib/pkgconfig/libgnurl.pc
}
