# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=giada
pkgver=0.16.3
pkgrel=1
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=('x86_64')
url="https://www.giadamusic.com/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libx11' 'libxpm')
# upstream vendors a hacked rtaudio :(
makedepends=('alsa-lib' 'fltk' 'gendesk' 'imagemagick' 'jack' 'libpulse'
'libsamplerate' 'libsndfile' 'nlohmann-json' 'rtmidi')
checkdepends=('catch2' 'xorg-server-xvfb')
source=("https://www.giadamusic.com/data/${pkgname}-${pkgver}-src.tar.gz"
        "${pkgname}-0.16.3-fix_tests.patch::https://github.com/monocasual/giada/commit/15cbe99dd12cec5bfce8029d9768755b1dcbbc9e.patch"
        "${pkgname}-0.16.3-fix_string_include.patch::https://github.com/monocasual/giada/commit/149d7b4b6c42e53cdd96890e4c635701521f52e7.patch"
        "$pkgname-0.16.2.2-devendor_nlohmann-json.patch")
sha512sums=('b2dab7a9263ce31dfa64bec2fb504ec26d762c3692a571ba25e11663dd3b3000ba6378044d8155458c969dfa38c8b23d9d135409fefec6a0943ed6137d73f0bf'
            '10d05fba9ffdf3836ccbec0bb7d381e9dd386a5959af784b36a3fba35d0e104db923998c99f2830053d5a8ed178ddb6501971e6f042a3461a4d08c0557325677'
            'bb553634619f3870b69d6e545352dd693db5e389f879769a70360a1aec6a627f97a0344648993083617d7cc64e812a81f717f0a2d33d47d2beecbb40fd302111'
            '31cf5b2b1bba29e8c97109c04c6456a98b778017ef5d48a0f020d326d8a11a5e76c8613fc232a2492d16aa48bc2faaae4aae7316252d6b887bd2187032fb83f9')

prepare() {
  mv -v "$pkgname-$pkgver-src" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Giada \
          --categories "AudioVideo;Audio;Midi;Sequencer"
  # devendor nlohmann-json
  patch -Np1 -i ../"$pkgname-0.16.2.2-devendor_nlohmann-json.patch"
  # fix tests and build: https://github.com/monocasual/giada/issues/351
  patch -Np1 -i "../${pkgname}-0.16.3-fix_tests.patch"
  patch -Np1 -i "../${pkgname}-0.16.3-fix_string_include.patch"
  # fixing test includes to use system catch2
  sed -e 's|catch\.hpp|catch2/catch\.hpp|g' -i tests/*.cpp
  # remove unused linking against X libs:
  # https://github.com/monocasual/giada/issues/352
  sed -e 's/-lXinerama//g' \
      -e 's/-lXcursor//g' \
      -e 's/-lXft//g' \
      -e 's/-lXrender//g' \
      -i Makefile.am
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  # TODO: build vst once the included juce is fixed for gcc >= 9.1.0
  # https://github.com/monocasual/giada/issues/328
  ./configure --prefix=/usr \
              --target=linux \
              --enable-system-catch
  make
}

check(){
  cd "$pkgname-$pkgver"
  xvfb-run -a make -k check
}

package() {
  depends+=('libasound.so' 'libfltk.so' 'libjack.so' 'libpulse.so'
  'libpulse-simple.so' 'librtmidi.so' 'libsamplerate.so' 'libsndfile.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # XDG integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "extras/${pkgname}-logo.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
