# Maintainer: David Runge <dave@sleepmap.de>
pkgname=giada
pkgver=0.15.4
pkgrel=1
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=('x86_64')
url="https://www.giadamusic.com/"
license=('GPL3')
groups=('pro-audio')
depends=('fltk' 'jansson' 'libpulse' 'libxpm' 'rtmidi')
makedepends=('gendesk' 'imagemagick')
checkdepends=('catch2' 'xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/monocasual/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('53575620d13d2bab57ab2431dfcc8967bcc6a1109e788bc52bef638268f314a75f8c9a6de2301de31fbc0eca578874e641d14bf6e26e02f4c156d970d36345dd')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Giada \
          --categories "AudioVideo;Audio;Midi;Sequencer"
  # fixing catch2 include for system library
  sed -e 's|catch\.hpp|catch2/catch\.hpp|g' -i tests/*.cpp
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --target=linux \
              --enable-system-catch
  make
}

check(){
  cd "$pkgname-$pkgver"
  xvfb-run -a make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # XDG integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "extras/${pkgname}-logo.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
