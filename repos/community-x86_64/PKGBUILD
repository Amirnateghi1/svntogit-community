# Maintainer: David Runge <dave@sleepmap.de>

pkgname=redkite
pkgver=0.6.1
pkgrel=2
pkgdesc="A small free software and cross-platform GUI toolkit"
arch=('x86_64')
url="https://gitlab.com/geontime/redkite"
license=('GPL3')
depends=('cairo')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/geontime/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('c6b9a6034430bbe9304a438626a9d885696f0d08931bdb7b3e461537ee19993f54f7c9fa81b461a40fd1def8c7ad7b5bfab3f34497373b240b96891b6c1482c0')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -pv build
}

build() {
  cd "$pkgname-$pkgver"
  (
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          ..
    make VERBOSE=1
  )
}

package() {
  cd "$pkgname-$pkgver"
  (
    cd build
    make DESTDIR="$pkgdir/" install
  )
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
