# $Id$
# Maintainer:
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>

pkgname=espeak
pkgver=1.49.0
pkgrel=2
pkgdesc='Text to Speech engine for English, with support for other languages'
arch=('x86_64' 'i686')
# The original espeak package looks unmaintained. espeak-ng is a direct continuation.
url='https://github.com/espeak-ng/espeak-ng/'
license=('GPL')
depends=('portaudio' 'libpulse')
# TODO: Add the sonic library to [community] and use it here.
makedepends=('git')
options=('!emptydirs')
source=("git://github.com/espeak-ng/espeak-ng#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd espeak-ng

  ./autogen.sh
}

build() {
  cd espeak-ng

  ./configure --prefix=/usr
  export CFLAGS="$CFLAGS -w --std=c11"
  make -j1
}

package() {
  cd espeak-ng

  make DESTDIR="$pkgdir" install
  ln -s /usr/lib/libespeak-ng.so "$pkgdir/usr/lib/libespeak.so"
  ln -s /usr/lib/pkgconfig/espeak-ng.pc "$pkgdir/usr/lib/pkgconfig/espeak.pc"
}

# vim:set ts=2 sw=2 et:
