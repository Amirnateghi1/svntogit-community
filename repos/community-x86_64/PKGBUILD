# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=libwebsockets
pkgver=4.1.4
pkgrel=1
pkgdesc="C library for websocket clients and servers"
arch=('x86_64')
url="https://libwebsockets.org"
license=('MIT')
depends=('glibc' 'openssl' 'libuv' 'libev' 'zlib')
makedepends=('cmake' 'glib2' 'libcap')
provides=('libwebsockets.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/warmcat/libwebsockets/archive/v$pkgver.tar.gz")
sha512sums=('4da84f612ad427465c4eea0bdd0db59c21c9e42bb21eb46b904f544dc4078af2c9a957e61430f3569b87cfc96b3addee50ac7a7c77777dcf16350fe441dd8854')
b2sums=('78cb9587af7a5cc368c3c4ce7b8c8cba7d25961595ae8853f52d374b73404e5fbd99ff9b552dc9d6370fd425d6bd28fc69b71be37f2f045817f2cb8f5354dcbc')

build() {
  cd "$pkgname-$pkgver"
  cmake -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_BUILD_TYPE='None' \
        -D LWS_WITH_HTTP2=ON \
        -D LWS_IPV6=ON \
        -D LWS_WITH_GLIB=ON \
        -D LWS_WITH_ZIP_FOPS=ON \
        -D LWS_WITH_SOCKS5=ON \
        -D LWS_WITH_RANGES=ON \
        -D LWS_WITH_ACME=ON \
        -D LWS_WITH_LIBUV=ON \
        -D LWS_WITH_LIBEV=ON \
        -D LWS_WITH_LIBEVENT=OFF \
        -D LWS_WITH_FTS=ON \
        -D LWS_WITH_THREADPOOL=ON \
        -D LWS_UNIX_SOCK=ON \
        -D LWS_WITH_HTTP_PROXY=ON \
        -D LWS_WITH_DISKCACHE=ON \
        -D LWS_WITH_LWSAC=ON \
        -D LWS_LINK_TESTAPPS_DYNAMIC=ON \
        -D LWS_WITHOUT_BUILTIN_GETIFADDRS=ON \
        -D LWS_WITHOUT_BUILTIN_SHA1=ON \
        -D LWS_WITH_STATIC=OFF \
        -D LWS_WITHOUT_CLIENT=OFF \
        -D LWS_WITHOUT_SERVER=OFF \
        -D LWS_WITHOUT_TESTAPPS=ON \
        -D LWS_WITHOUT_TEST_SERVER=OFF \
        -D LWS_WITHOUT_TEST_SERVER_EXTPOLL=ON \
        -D LWS_WITHOUT_TEST_PING=ON \
        -D LWS_WITHOUT_TEST_CLIENT=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libcap.so' 'libglib-2.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
