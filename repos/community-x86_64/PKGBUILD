# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=redkite
pkgver=1.2.0
pkgrel=1
pkgdesc="A small free software and cross-platform GUI toolkit"
arch=('x86_64')
url="https://gitlab.com/iurie-sw/redkite"
license=('GPL3')
depends=('glibc' 'cairo')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/iurie-sw/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('4fc8a31b2a900b0e19452d57b53987725fba793839b4b0c0f3bb9be7b09db84858dd39f712ea75cf5599e72ac16b817656d32349f4b5bcae09a5a48c2cd4df26')
b2sums=('3056046c9454726c7f65397658141fb282ecfeb9187a8a7b19cb73c9399f69bad8f551e4dc04c6f4247658900779d300d752c0befbe334c6835a3c173b799dd5')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
