# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=Kvantum
pkgname=kvantum-qt5
pkgver=0.10.10
pkgrel=1
pkgdesc="SVG-based theme engine for Qt5 (including config tool and extra themes)"
arch=('x86_64')
url="https://github.com/tsujan/Kvantum"
license=('GPL')
depends=('qt5-base' 'qt5-svg' 'qt5-x11extras' 'libx11' 'libxext' 'hicolor-icon-theme' 'kwindowsystem')
makedepends=('cmake' 'qt5-tools')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/V${pkgver}.tar.gz")
sha256sums=('e21fb70ff3260d83b317d0902a0468a7446d840b632d043ee860597501529a09')

prepare() {
    mkdir -p build
}

build() {
    cd build
	cmake ../${_pkgname}-${pkgver}/${_pkgname} \
	      -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
    # KDE4 is long dead
    rm -r "${pkgdir}"/usr/share/kde4
}
