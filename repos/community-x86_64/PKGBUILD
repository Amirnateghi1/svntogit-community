# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: loqs

pkgname=rust-libslirp
pkgver=4.2.2
pkgrel=1
pkgdesc='High-level bindings & helper process for libslirp.'
url='https://gitlab.freedesktop.org/slirp/libslirp-rs'
arch=('x86_64')
license=('MIT')
depends=('dbus' 'libslirp')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://crates.io/api/v1/crates/libslirp/${pkgver}/download")
b2sums=('890f0758d6315f6b00fc06be86ec449aa92bfe6af38d05f92396cbaec46bd4307618164556ebf5e009b4012ecc7a72d6df5e22a5bb88f00b9433efdeaa50ab18')

build() {
  cd ${pkgname#rust-}-${pkgver}
  cargo build --release --locked --all-features --target-dir=target
}

package() {
  cd ${pkgname#rust-}-${pkgver}
  install -Dm755 "target/release/libslirp-helper" "${pkgdir}"/usr/bin/slirp-helper
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
