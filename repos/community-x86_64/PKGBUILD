# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: poisonby <poisonby@tutanota.com>
# Contributor: Jason Scurtu (scujas) <jscurtu@gmail.com>
# Contributor: Marcin Tydelski <marcin.tydelski@gmail.com>
# Contributor: Jan Lukas Gernert (JeanLuc) <https://launchpad.net/~eviltwin1>

pkgname=feedreader
_pkgname=FeedReader
pkgver=2.2
pkgrel=1
pkgdesc="Modern desktop application designed to complement existing web-based RSS accounts"
arch=(x86_64)
url="https://jangernert.github.io/FeedReader/"
license=(GPL3)
depends=(curl gnome-online-accounts libgee libpeas)
makedepends=(git gobject-introspection meson vala)
_commit=61f6331e700421ca917a309df9efa2e119890ba5  # tags/v2.2
source=("git+https://github.com/jangernert/$_pkgname#commit=$_commit"
        "git+https://github.com/fivefilters/ftr-site-config")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname
  git config submodule.data/GrabberConfig.url "$srcdir/ftr-site-config"
  git submodule update --init

  # Revert bad commit
  # https://github.com/jangernert/FeedReader/issues/677
  git revert -n ccb7a52d67d9fe78f663cf6cc991ea51882f453e
  git revert -n b5ab26de2928e011ceafeb87e0f20131764e7021
}

build() {
  arch-meson $_pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
