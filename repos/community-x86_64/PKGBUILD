# Maintainer: David Runge <dave@sleepmap.de>

pkgname=redkite
pkgver=0.6.2
pkgrel=1
pkgdesc="A small free software and cross-platform GUI toolkit"
arch=('x86_64')
url="https://gitlab.com/geontime/redkite"
license=('GPL3')
depends=('glibc' 'cairo')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/geontime/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('dde01be8d5762eed6c6af621c4f287248b51ed679ec8791f59a4083be1631c560e7323a4bdcc24d6cc7cb9c3cc2957f4352370ccaa72ded5442a069e59bb0cfb')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -pv build
}

build() {
  cd "$pkgname-$pkgver"
  (
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          ..
    make VERBOSE=1
  )
}

package() {
  cd "$pkgname-$pkgver"
  (
    cd build
    make DESTDIR="$pkgdir/" install
  )
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
