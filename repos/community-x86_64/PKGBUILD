# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=bfgminer
pkgver=3.1.0
pkgrel=1
pkgdesc="Bitcoin miner featuring overclocking, monitoring, fan speed control and remote management. For FPGA/GPU/CPU Bitcoin mining."
arch=('i686' 'x86_64')
depends=('curl' 'jansson' 'libusbx' 'lm_sensors')
makedepends=('uthash' 'yasm')
optdepends=('opencl-nvidia: OpenCL implementation for NVIDIA')
url='https://bitcointalk.org/index.php?topic=168174.0'
license=('GPL3')
options=(!libtool)
source=(http://luke.dashjr.org/programs/bitcoin/files/$pkgname/$pkgver/$pkgname-$pkgver.tbz2
        remove-dangerous-rpath.patch)
sha256sums=('2f0537c361c1e2e41afff788d7a10e527a555c8d1d63f7997b3fa29afcdf4c19'
            'c47eb443b870262e3e1a5d6441a06f72c9ca06c0abae459ab577a3f289a91e7a')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -p0 < "${srcdir}"/remove-dangerous-rpath.patch
  NOSUBMODULES=1 ./autogen.sh
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-cpumining --enable-scrypt
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
