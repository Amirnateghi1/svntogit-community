# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=box2d
pkgver=2.3.1
pkgrel=5
pkgdesc="2D rigid body simulation library for games"
url="http://www.box2d.org/"
license=('zlib')
arch=('x86_64')
#depends=('freeglut')
makedepends=('cmake' 'doxygen' 'subversion')
optdepends=()
source=("https://github.com/erincatto/Box2D/archive/v${pkgver}.tar.gz")
md5sums=('20f1e128f99ddb1634c2ba55a959feb2')

prepare() {
  mkdir box2d-${pkgver}/Box2D/build
}

build() {
  cd box2d-${pkgver}/Box2D/build

	cmake .. \
    -DBOX2D_INSTALL=ON \
    -DBOX2D_INSTALL_DOC=ON \
    -DBOX2D_BUILD_SHARED=ON \
    -DBOX2D_BUILD_STATIC=OFF \
    -DBOX2D_BUILD_EXAMPLES=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd box2d-${pkgver}/Box2D/Documentation

  doxygen

  cd ../build

  make DESTDIR=${pkgdir} install

  install -Dm644 ../License.txt ${pkgdir}/usr/share/licenses/box2d/LICENSE
}

# vim: sw=2 ts=2 et:
