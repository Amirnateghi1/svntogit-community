# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=redkite
pkgver=1.0.1
pkgrel=1
pkgdesc="A small free software and cross-platform GUI toolkit"
arch=('x86_64')
url="https://gitlab.com/iurie-sw/redkite"
license=('GPL3')
depends=('glibc' 'cairo')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/iurie-sw/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('37da5244d9868c89a49fbdeac81c54e716dba46bebe681607b6c165c6c9e4e4b3ce94237794be66220954eb9087395baf80dae608ea163b58f2f353345de99fb')
b2sums=('a60271672036a86f0c39a4fec112bcc732add1c78f275b17e15b69853cb33d019f89a8051eea8ca7c155ffbb5cb378452cfa73a2ebd0a7af860c92b5262354db')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
