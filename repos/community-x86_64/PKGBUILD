# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=tpm2-tss
pkgver=2.1.0
pkgrel=1
pkgdesc="Implementation of the TCG Trusted Platform Module 2.0 Software Stack (TSS2)"
arch=('x86_64')
url="https://github.com/tpm2-software/tpm2-tss"
license=('BSD')
depends=('uriparser' 'libgcrypt' 'uthash')
checkdepends=('openssl' 'cmocka' 'iproute2' 'ibm-sw-tpm2' 'procps-ng')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'tpm2-tss-1234.patch') # From https://patch-diff.githubusercontent.com/raw/tpm2-software/tpm2-tss/pull/1234.patch
sha256sums=('a50b8dbd51f0c274cf874351786ed252c5024c952bf699fbd595ac292a27ab19' 'SKIP'
            '0998c82d37ff5d3f521b978bfd3c01ca83ec82e020cff4807b03d1935bccd7fe')
validpgpkeys=('D760B790CCF0A41CBE7B047C316CC1FB24ABDC72') # Tadeusz Struk <tadeusz.struk@intel.com>

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../tpm2-tss-1234.patch
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr --with-udevrulesprefix=60- --enable-unit --enable-integration
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
    echo 'u tss - "tss user for tpm2"' | install -Dm644 /dev/stdin "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
}
