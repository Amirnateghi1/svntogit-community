# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=17.01
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}" "eric-common-qt4>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_locale} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common${2}>=${pkgver}")

  [ -z ${2} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('1ff4ae708ce0fa494df1d315f939d461'
         '8c6bcc41b310f21db85d4aa9763c57cf'
         '262e54e382c964fd93ae5ffc2a5d22db'
         'c5885e837ab08b1bc0224131342b37c9'
         'a1bb7b36d63eb95d4e3baf73ff8e4058'
         '4bf5fb9ce99acde9149160f721fcdcbf'
         '8f0fbe63aa138bd46c23b8ca5349b379'
         '9af90303b3bd9099339f4bf4c2ab16e2'
         'bd66bf3c283427f117f0004f7fb1c8a0'
         'aacbf46c05fbe420d56131378cea0b3d')
