# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.1.4
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_locale} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common${3}>=${pkgver}")

  [ -z ${3} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('62a9eb1130a18ba399d4627a6ed8d6b1'
         '6c2a7f60ec0a72ce12535860a4e261a5'
         'a5831b6ce88bf901c4385c95d191df00'
         '0a796aa412c14864a9742bca74047cbe'
         '9297eba27c43909db0d29de37a95cc04'
         '20e64c0cd99935396258c1b3bb401e87'
         '041137831502bc1a80b6e2258372d6b9'
         '6642d46307a108952a7f0e00e4c23ded'
         '54cced0aac6c8b1b825fa5ce0aee96d9'
         '1237f2f6bc83d42d5f4a35359e544907')
md5sums=('ace3799cdcd38f5c91167d72b4250780'
         '7ff46ddea7304b06bc80ec3aea0986e7'
         '5e1ce5ce9e85b0d80dac11e6d81e3f52'
         '9e77c42195b932cf41f4e200fbfcec66'
         'a86b6986e855881ef658dbd72c2ebc44'
         'e4402d9861676486e06a2afa64a1831b'
         'f5f58ef92554cec2ae98bced1f015e63'
         '369828aeb70243467361323a3606b7b1'
         '8598a0af5498d263204ac481a3c521ea'
         'a148c6589d806340bf5e909c78fbaab7')
