# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Ray Rashif <schiv at archlinux dot org>
# Contributor: Brad Fanella <bradfanellaat archlinux dot us>
# See .contrib for older/other contributors

pkgbase=eric
pkgname=('eric-common' 'eric' 'python2-eric')
pkgver=17.11
pkgrel=1
pkgdesc="A full-featured Python and Ruby IDE in PyQt"
arch=('any')
url="http://eric-ide.python-projects.org/"
license=('GPL3')
depends=('python-qscintilla' 'python2-qscintilla'
         'python-qscintilla-qt5' 'python2-qscintilla-qt5'
         'qt5-svg' 'qt5-webkit')
source=("https://downloads.sourceforge.net/eric-ide/eric6-${pkgver}.tar.gz")
sha512sums=('e95941805d44f01de67d5f256f822a829bd224841a7f2b0bd245e7388b96a5eb94ab34004720c05a355fbeeb30ed4a521e07ea7069441fe6a90c13ff8cf853f8')
changelog=ChangeLog

prepare() {
  cp -r "${srcdir}/eric6-${pkgver}"{,_py2}

  cd "${srcdir}/eric6-${pkgver}_py2/eric"
  find . -name '*.py' -exec \
    sed -i -r "s:^#!/usr/bin/(env |)python(3|)$:#!/usr/bin/env python2:" {} \;
}

package_eric-common() {
  depends=()

  cd "${srcdir}/eric6-${pkgver}/eric"
  # eric APIs
  install -Dm644 "APIs/Python3/eric6.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/eric6.api"
  install -Dm644 "APIs/Python3/eric6.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/eric6.bas"
  # pyqt APIs
  install -Dm644 "APIs/Python3/PyQt5.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/PyQt5.bas"
  # qss APIs
  install -Dm644 "APIs/QSS/qss.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/qss/qss.api"
  # qscintilla APIs
  install -Dm644 "APIs/Python3/QScintilla2.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/QScintilla2.bas"
  # zope APIs
  install -Dm644 "APIs/Python/zope-2.10.7.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-2.10.7.api"
  install -Dm644 "APIs/Python/zope-2.11.2.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-2.11.2.api"
  install -Dm644 "APIs/Python/zope-3.3.1.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-3.3.1.api"
  # ruby APIs
  install -Dm644 "APIs/Ruby/Ruby-1.8.7.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.api"
  install -Dm644 "APIs/Ruby/Ruby-1.8.7.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.bas"
  install -Dm644 "APIs/Ruby/Ruby-1.9.1.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.9.1.api"
  install -Dm644 "APIs/Ruby/Ruby-1.9.1.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.9.1.bas"
}

package_eric() {
  depends=('eric-common' 'python-qscintilla-qt5' 'qt5-svg' 'qt5-webkit')
  conflicts=('eric-qt4')

  cd "${srcdir}/eric6-${pkgver}"
  python install.py -c -b /usr/bin -i "${pkgdir}" --noapis
}

package_python2-eric() {
  depends=('eric-common' 'python2-qscintilla-qt5' 'qt5-svg' 'qt5-webkit')
  conflicts=('python2-eric-qt4')

  cd "${srcdir}/eric6-${pkgver}_py2"
  python2 install.py -c -b /usr/bin -i "${pkgdir}" -y --noapis
}

# vim:set ts=2 sw=2 et:
