# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=termtosvg
pkgver=0.8.0
pkgrel=1
pkgdesc='Record terminal sessions as SVG animations'
arch=('any')
url=https://nbedos.github.io/termtosvg/
license=('BSD')
depends=('python-lxml' 'python-pyte')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('a414060090e15419e96c481f1aee2882669d4a804cab84132e628496c8f1f0c1be86e48dff7e3614a6d9060c80a3dfbbab1b6b54044d306fe022e825b6bb5391')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python -m unittest termtosvg.tests.suite
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 man/termtosvg.man.1 "$pkgdir"/usr/share/man/man1/termtosvg.1
  install -Dm644 man/termtosvg-templates.man.5 "$pkgdir"/usr/share/man/man5/termtosvg-templates.5
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
