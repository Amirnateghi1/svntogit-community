# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=17.08
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide/"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

md5sums=('1708ae75004e4b630d91468098982e12'
         '92f66a3cad3513e10ccf8475f7bb9284'
         '57ec0fae44a1dff4069ce209f95bed5b'
         '97cae64c200f0ac4a225cbe318783440'
         '5172212c633b9eaad6ac1c1fec509c49'
         'db1da232daf6a9710b003d7ceda574fe'
         'f23a24aaaef1bee1ac15cfaaff8c3b84'
         'a3a9e479448b7dcb50c218ed0c1bebd7'
         'a3363137f8d198b6d9692a8246437b32'
         'ef7ec6378972b37990b4a8f9bccf813d')
