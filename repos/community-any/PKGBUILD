# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=freemind
pkgver=0.9.0
pkgrel=1
pkgdesc="A Java mindmapping tool"
arch=(any)
url="http://sourceforge.net/projects/freemind"
license=('GPL')
makedepends=('unzip')
depends=('java-runtime')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-bin-max-$pkgver.zip
        freemind.png
        freemind.desktop
        freemind.run)
md5sums=('1a346f1a3aabb2db7e6685bb3844797b'
         '82a474f26f976974b95da2e25b7480a0'
         '07241361af6cfb965deeab890d17f4a7'
         '4070fc9f286ad7aaa4eb394203152b33')

package() {
  cd "$srcdir"

  for file in $(find lib plugins -type f); do
	  install -Dm644 $file "$pkgdir/usr/share/freemind/$file"
  done

  install -Dm755 "$srcdir/freemind.run" "$pkgdir/usr/bin/freemind"
  install -Dm755 "$srcdir/freemind.sh" "$pkgdir/usr/share/freemind/freemind.sh"
  install -Dm644 "$srcdir/patterns.xml" "$pkgdir/etc/freemind/patterns.xml"
  #install -Dm644 "$srcdir/user.properties" "$pkgdir/usr/share/freemind/user.properties"

  install -d "$pkgdir/usr/share/freemind/accessories"
  install -Dm644 "$srcdir"/accessories/* "$pkgdir/usr/share/freemind/accessories/"
  install -Dm644 "$srcdir/doc/freemind.mm" "$pkgdir/usr/share/freemind/doc/freemind.mm"

  install -Dm644 "$srcdir/freemind.desktop" "$pkgdir/usr/share/applications/freemind.desktop"
  install -Dm644 "$srcdir/freemind.png" "$pkgdir/usr/share/pixmaps/freemind.png"
}

# vim:set ts=2 sw=2 et:
