# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.1.11
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}" "eric-common-qt4>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_locale} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common${2}>=${pkgver}")

  [ -z ${2} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('f47e4571564a93ab87940fef56cb1202'
         'd2ea34c3c73b62e29c463b581a0157ad'
         '7a6f22caf969361052b37233b1c0abc5'
         '5dadecb4efcf8fff41eaee5c0f724055'
         'bbf903d7905f9118cb6bf337ed1c01d5'
         '0532d907d42d234b28e91aff13376157'
         '298f3e3549a0852ee4223cac9fbbb659'
         'a1d10485cd5691d27007fbcd0d3e203d'
         '7e5d53ff8be66a70740e56287c4f7f19'
         '98f0e8160eee260c11d89e65ab502b42')
