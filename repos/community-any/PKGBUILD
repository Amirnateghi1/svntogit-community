# Maintainer: Ivy Foster <iff@archlinux.org>
# Contributor: Alad Wenter <alad@mailbox.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>

pkgname=youtube-viewer
pkgdesc='Command line utility for viewing YouTube videos'
url='https://github.com/trizen/youtube-viewer'
license=(GPL3)

epoch=1
pkgver=3.7.4
pkgrel=1

depends=(perl perl-data-dump perl-file-sharedir perl-json perl-lwp-protocol-https perl-libwww)
makedepends=(perl-module-build)
optdepends=(
	'gtk2-perl: GTK2 GUI'
	'mpv: play videos with MPV'
	'perl-file-sharedir: GTK GUI'
	'perl-gtk3: GTK3 GUI'
	'perl-json-xs: faster JSON to HASH conversion'
	'perl-term-readline-gnu: better STDIN support'
	'perl-text-charwidth: print results in a fixed-width format'
	'vlc: play videos with VLC'
	'wget: download videos with wget'
	'youtube-dl: play videos with encrypted signatures'
)

arch=(any)
source=("$pkgname-$pkgver.tar.gz::https://github.com/trizen/$pkgname/archive/$pkgver.tar.gz")
# sums provided by packager makepkg -g
sha256sums=(f049fe7f6f27c1dbc4e1835f2b99c922b7d66b11568edd63653c670ce5500cfd)

build() {
	cd "$pkgname-$pkgver"
	perl Build.PL --installdirs vendor --gtk
}

check() {
	cd "$pkgname-$pkgver"
	./Build test
}

package() {
	cd "$pkgname-$pkgver"
	./Build install --destdir "$pkgdir"
	rm -r "$pkgdir/usr/lib"

	mkdir -p "$pkgdir/usr/share/applications" "$pkgdir/usr/share/pixmaps"
	mv "$pkgdir/usr/share/perl5/vendor_perl/auto/share/dist/WWW-YoutubeViewer/gtk-youtube-viewer.desktop" \
		"$pkgdir/usr/share/applications/gtk-youtube-viewer.desktop"
	cp "$pkgdir/usr/share/perl5/vendor_perl/auto/share/dist/WWW-YoutubeViewer/icons/gtk-youtube-viewer.png" \
		"$pkgdir/usr/share/pixmaps/gtk-youtube-viewer.png"
}
