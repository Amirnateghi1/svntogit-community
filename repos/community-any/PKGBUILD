# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.1.9
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}" "eric-common-qt4>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_locale} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common${2}>=${pkgver}")

  [ -z ${2} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('ab34d8e8b7b96dd0921023fa286de3f5'
         'dd931b7873035e029c6bd8aa3f7be341'
         'de061bffbbb3ac2f2f90060f4c9f684a'
         '5eb0413e54c847eea60e8219b792affd'
         '1419f06a78eac7497f0b3197eb858c1f'
         'c5b1bea2388d7ea802ac534ef4744990'
         '8d00511cb71c1baba2623e63fbbd02d9'
         'db02dbba15dffcde490f28cf8b85fe41'
         '69393d73b4dec375ed7b7154e1cfd7d6'
         'b1e508f50bb9ca0abe907edb4aab3c14')
