# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: FUBAR <mrfubar@gmail.com>

pkgname=shorewall
pkgver=4.4.6
pkgrel=1
pkgdesc="An iptables-based firewall for Linux systems"
arch=('any')
url="http://www.shorewall.net"
backup=(etc/shorewall/{shorewall.conf,zones,functions,policy,interfaces,ipsec,hosts,rules,nat,netmap,params,proxyarp,routestopped,maclist,masq,modules,tcclasses,tcdevices,tcfilters,tcrules,whitelist})
license=('GPL')
depends=('iptables' 'iproute2')
replaces=('shorewall-perl')
source=(http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.bz2)
md5sums=('fbc2605c946196928e38519161ebf28b')

build() {
  cd ${srcdir}/$pkgname-$pkgver
 
  sed -e 's!LOGFILE=/var/log/messages!LOGFILE=/var/log/messages.log!' -i configfiles/shorewall.conf
  sed -e 's!LOGFILE=/var/log/messages!LOGFILE=/var/log/messages.log!' -i shorewall

  mv init.archlinux.sh init.sh
  DEST=/etc/rc.d INIT=shorewall PREFIX=${pkgdir} ./install.sh
  cp -rv ${srcdir}/$pkgname-$pkgver/Samples ${pkgdir}/usr/share/shorewall

  sed -e 's!/subsys!!' -i ${pkgdir}/etc/shorewall/shorewall.conf

  for file in ${pkgdir}/etc/shorewall/*; do
    chmod 644 ${pkgdir}/etc/shorewall/$(basename $file)
  done
  chmod 644 ${pkgdir}/usr/share/shorewall/modules
}
