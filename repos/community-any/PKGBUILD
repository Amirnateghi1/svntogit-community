# Maintainer: Alad Wenter <alad@archlinux.org>
# Contributor: John Jenkins twodopeshaggy@gmail.com
# Contributor: Sean Anderson seanga2@gmail.com
pkgname=('python-kitchen' 'python2-kitchen')
pkgbase=python-kitchen
pkgver=1.2.5
pkgrel=3
pkgdesc="Useful snippets of python code"
url="https://github.com/fedora-infra/kitchen"
makedepends=('python-setuptools' 'python2-setuptools')
license=('LGPL')
arch=('any')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('1258af80868fc8c30170a43040b1eb5434bc5ffa5ff4ab98ffb9ee3d3c1e2f76')

build() {
    cd "kitchen-$pkgver"

    # kitchen has separate source trees for python2 and python3
    python setup.py build
    python2 setup.py build
}

package_python-kitchen() {
    depends=('python')
    cd "kitchen-$pkgver"

    python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-kitchen() {
    depends=('python2')
    cd "kitchen-$pkgver"

    python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
    sed -i 's/python/python2/' "$pkgdir/usr/lib/python2.7/site-packages/kitchen/pycompat24/base64/_base64.py"
}
