# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.1.2
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale} eric-qt4-i18n-${_locale})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale}() {
      _package ${_locale} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${2} language pack for Eric IDE"
  depends=("eric-common${3}>=${pkgver}")

  [ -z ${3} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('bf362f39bc94231849f3c18bd5398a73'
         'dffb2de2c5afcb952ae5937bdac7d3fa'
         '9daf265f237412df09e3c4f8ff7a56a6'
         '61add709c3772b8cf55b7fb1b1a70efb'
         'f89a21a68c0247dc8430bad59e7c6d04'
         'eb0916c9333883873db8490953bdacfc'
         'd7a4792a1ae7bc49152634d6e09ab7f0'
         'e7f48edea4089827c87e3e6a6126c83f'
         '1d4b5ff1b5720156e78ba3e7f2a95341'
         '4e1bc3da7a950230e53038f4f40412d3')
