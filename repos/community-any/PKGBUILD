# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=18.06
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('18bb370b9dfd8a9fbdabaf3663441e0c795eca529e943c3749ca3d4cd3ca1bb4c8594c8dbad8a557e297725d5db6e8d9ef44eb3df7298a24e19d6d2f357b6a9c'
            'b78b3978afa7f7063445ac49b707f119e56d9db75a8050700bb63d3b5460fbd3a86ab999502c2433a9e48294174383789806ffbc6d60158046dfb1c7f451871b'
            'c963d32caa3cc640c745b1f96d800bf3a01e6ec1d6d60db34e5f5cb5ba1833c4f49de31002bdc142e7d88bf6d1f79117b8a30b96086d3d9fba3ff170b55a427c'
            '3f5aac30a6897023151a88048102299b5770df1d79f2fa8297a3d5622219b3b126d02394e9ae2eeb872a3861cc85268faad867cdc3e131671e65ff3576a5aa1e')
