# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Tom <reztho at archlinux dot us>

pkgname=pdfsam
pkgver=4.0.1
pkgrel=1
pkgdesc="A free open source tool to split and merge pdf documents"
arch=('any')
url="http://www.pdfsam.org/"
license=('AGPL3')
depends=('java-environment>=11' 'sh')
makedepends=('desktop-file-utils' 'gendesk')
source=("https://github.com/torakiki/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}-linux.zip"
        "pdfsam"
        "pdfsam.png")

prepare() {
  # create *.desktop file
  gendesk -f -n \
          --pkgname="${pkgname}" \
          --pkgdesc="${pkgdesk}" \
          --name="PDFsam" \
          --categories="Office"
}

package() {
  install -dm755 "${pkgdir}/usr/share/java/${pkgname}/lib"
  install -Dm644 "${pkgname}-${pkgver}-linux/pdfsam-basic-${pkgver}.jar" \
                 "${pkgdir}/usr/share/java/${pkgname}/"
  install -Dm644 "${pkgname}-${pkgver}-linux/lib/"* \
                 "${pkgdir}/usr/share/java/${pkgname}/lib"
  install -Dm755 "${pkgname}-${pkgver}-linux/bin/pdfsam.sh" \
                 "${pkgdir}/usr/share/java/${pkgname}/bin/pdfsam.sh"

  # exec
  install -Dm755 "pdfsam" "${pkgdir}/usr/bin/pdfsam"

  # desktop
  install -Dm644 "pdfsam.png" "${pkgdir}/usr/share/pixmaps/pdfsam.png"
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

sha512sums=('2ae09a8dca04fd6f1b8b9773b0e12ce68803b2d4f5b231ae25ea1a64f1dcc054b4e9e159418337d4376afecd5adb02635368d99921ea1313832a9e3e6587fbd8'
            '8be5dd1d9da3c0d646db88b12857f8e7c857447dc9f5344028ecb299b33a08723ab7b1d2b8d5582cee5d85eb632a907c4d153fb1593265dfc01f64b6b1af83bd'
            '219c3b78b1246103ca6c0b4b50f23a38f994458f54ddfdd37058df33587e258bbe0c3b7f0866dc3d10e657c9425a8b3be915ef6a09a2401e937ac50315653dfb')
