# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=18.03
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('e2ffcc4a06204c12864871f45ad76169c5aa47d82bbba03dcb4a6d9ea487745ce0b065af0af4d09ba11cc8d05c1ab734b70df3df3de57232ee6c045a5cf9092a'
            '4b52e7dcbe4120f33a839172cf5a4b60e996b5a617b61b57abdea8bd6b9a2f4be6847f8aa7d6738c0c477031d33e7bc89d269adff0a0dd749b32c7b7aad46709'
            '102e6946ad33c2d1dba697c12d5aaec3fa61010a0118fd5548a8c73cd8b8ded1d76e64c3e9d83fbb4d95589720c1449bdfaadb06117f3d65cdd2c9572ec43dcc'
            '7a7aaaa5eedf8bb81573d97465b13ee6dc3d2846a4074a3094f401223c890f52c05c9852bf6f440aa5354621f25ce1aa461efb38b8ad2136ba1bae3a21b4497d')
