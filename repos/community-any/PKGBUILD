# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.1.10
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}" "eric-common-qt4>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN          "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_locale} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common${2}>=${pkgver}")

  [ -z ${2} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('79ae2beefa5cba89557c3bce2999878d'
         '8708d5fa14a8afe87db7ae9519049072'
         '500de6d8245d024bf8879e570e263435'
         '1f327003db4c8f0aa082461727a13b68'
         'f40936d1b9211ddce31af98f903ce899'
         '53e379a86ea430b30af372f5fcd9a00b'
         '8626e674de88f5b9e021da438aca0e81'
         'fd6c33330add2d5f3556043dc345d3c5'
         '930e74b0689df8a5960c8b7963d01e93'
         '06b7a5e59649eef7c75c451fb8bbdc0d')
