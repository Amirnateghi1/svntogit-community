# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Dave Pretty <david dot pretty at gmail dot com>

pkgname=anki
pkgver=2.1.0beta40
pkgrel=1

pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently"
url="https://ankisrs.net/"
arch=('any')
license=('AGPL3')

depends=('qt5-webengine' 'python-beautifulsoup4' 'python-pyaudio'
         'python-pyqt5' 'python-requests' 'python-send2trash'
         'python-decorator' 'python-markdown')
optdepends=('lame: record sound'
            'mplayer: play sound')

source=(https://apps.ankiweb.net/downloads/beta/anki-$pkgver-source.tgz
        0001-aqt-webview-Make-acceptNavigationRequest-accept-data.patch
        0002-Stop-checking-Qt-version.patch
        0003-Create-a-widget-for-AnkiWebView.patch)
sha256sums=('a9d03adfb40f66e9e3f8c71ea19c5406ae65d80e31f60bad7f12f65eefa1b52f'
            '7744c877601badd705c7f6f7406ccc089c3548ccd413b8d97def90c16a34923e'
            '45082bf74e9fa55b48558dab112fcd132f46a33af0f3435781beb16e5cc328c2'
            '75afd8e0f23b5e84083c92585598d26d4c3be461ba4981b0aaf995747ed47e59')

prepare() {
  cd anki-$pkgver

  patch -p1 <"$srcdir"/0001-aqt-webview-Make-acceptNavigationRequest-accept-data.patch
  patch -p1 <"$srcdir"/0002-Stop-checking-Qt-version.patch
  patch -p1 <"$srcdir"/0003-Create-a-widget-for-AnkiWebView.patch
}

package() {
  cd anki-$pkgver

  make install DESTDIR="$pkgdir"

  sed -i '$ iimport os\nos.environ["QTWEBENGINE_DISABLE_SANDBOX"] = "1"' "$pkgdir"/usr/bin/anki
}
