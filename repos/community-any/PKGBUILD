# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=18.11
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('d01510433531b28a3f5650630217404a8b428f1130bb3f5a5d208f632c25947ebb0cff2412e3fcd988f825c275452f31418c8bd375757891d89581c6d388b7d1'
            '34ffbe447cb104ccaf196fc5f3e4daa1250a93ec44058d1f3530390652379ff898a04e08cb038d9de306316799fbdefa3ee021e4b732725eef037cebf3c53f6b'
            '52f1ef0cafab5592debfa053aab362a2d71f2daa1c21841beb0343a05dcc38ab16a29be26580853f1005aa8777166c33fa0edfa2a07f7cf77d10abc1fc03b094'
            '37645bd84a640a11123130e59aa92df426da7d957f2e14261dc88fe4b83a42e66ae1dc04a9102bd1b77e80a4dee1316daf95f0ea188cce91c85b8e5bcb80a947')
