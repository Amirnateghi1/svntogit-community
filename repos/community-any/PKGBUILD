# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=18.04
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('cc4a449c01b7c11422689440f624c543ef65cdb29ae8d7155e4f39308f65c85551d0130b0325e47f494eb4fa76f3134427dc294dc1412934752346ff5f4491cd'
            'b1e7da29ca06992f139ca3e27050dbd1dba28a4bcca8e93310036d167242c7b5c8ada597c49a1fb82407733f2d1d9fbd5ebcc54d2e0362c56e16b84b80c6d956'
            '3177604057b37e53f73fa2c80d18afde640d54668e04c2e16bde9a3da564dbdf3c6de2cbe358cb7dcce08a00ae75c4009c8b2b7e99a8af4246f68da571c5ce3e'
            'ad9cfa0adf1f312d42d7d0ec9fb0f30faa278be10116b68d0636e4a70e9ed84c6f2c499e4247df48419205e9bbfbbb58e55f6de0f4925a9960a495b1d5e51102')
