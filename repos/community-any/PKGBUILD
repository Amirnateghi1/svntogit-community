# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=19.06
_pkgver=19.6
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${_pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${_pkgver}/eric/eric6/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('96d3090777208aea953fbd9cfd2ff7d4b0e7de414f538a5094ef74c85a3a394204089076eda5c13f221aef3f9468dd93086f677ca22dc2e3d1f53e37935dfd77'
            '5389d9d58476c5525f5da8b8f004124622f6d3c2787e7d142c0a30fee5a19455e5e440f217cd19984477968c45bb5c321b28829d3bbfe6d07d4a02d77a60b3f3'
            '5403b04abdc144dd144994159d34eab629da955e45f0d91f521e70d39f033141680f4df4a78b840074560ce75c9771d00a8be65022acfcc645774a9b6d749fca'
            'e22637d3fdb78b7ec21a3a3b4c4b5a09fd881942054062dbe0583d2008e5cedd7307c61bda62440400a838dc93f0a7c5f82d1e3fa531b5948930aaa2be193141')
