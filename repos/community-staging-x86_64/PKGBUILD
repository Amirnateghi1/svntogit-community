# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: m0rph <m0rph.mailbox@gmail.com>

pkgname=goldendict
pkgver=1.5.0RC
pkgrel=3
pkgdesc="Feature-rich dictionary lookup program"
arch=('i686' 'x86_64')
url="http://goldendict.org"
license=('GPL3')
depends=('hunspell' 'libxtst' 'libzip' 'libao' 'qtwebkit' 'phonon-qt4' 'ffmpeg')
provides=('stardict')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/goldendict/goldendict/archive/1.5.0-RC.tar.gz
        $pkgname-paths.patch
        Fix-compilation-with-last-FFMpeg-versions-issue-673.patch)
sha256sums=('5705d5197509596221faf16a806965c799de7371a41fe2d2c4dc00b737ffe22e'
            '5281dc2f57a93e2aa416de35bd702926bd90b4746d62f81d8dc434f6a689d55e'
            '80bffcac787445ef272c06dfe4b73ab09da4a00733404198f60f25f2bb9edba9')

prepare(){
  cd "${srcdir}"/$pkgname-1.5.0-RC

  patch -Np0 -i "${srcdir}"/$pkgname-paths.patch
  patch -Np1 -i "${srcdir}"/Fix-compilation-with-last-FFMpeg-versions-issue-673.patch
}

build(){
  cd "${srcdir}"/$pkgname-1.5.0-RC

  qmake-qt4 PREFIX="/usr"
  make
}

package() {
  cd "${srcdir}"/$pkgname-1.5.0-RC

  make INSTALL_ROOT="${pkgdir}" install

# languages
  install -d "${pkgdir}"/usr/share/$pkgname/locale
  install -m644 locale/*.qm -t "${pkgdir}"/usr/share/$pkgname/locale

# removing unneeded
  rm -rf "${pkgdir}"/usr/share/app-install
}
