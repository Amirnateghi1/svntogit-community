# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=pynac
pkgver=0.6.6
pkgrel=1
pkgdesc="Python version of GiNaC, a C++ library for symbolic mathematical calculations"
arch=(i686 x86_64)
url="http://pynac.org"
license=(GPL)
depends=(python2 libgiac)
source=("https://github.com/pynac/pynac/archive/$pkgname-$pkgver.tar.gz"
        pynac-giac.patch::"https://github.com/pynac/pynac/commit/101ea20b.patch")
md5sums=('413e071cfeafbc0005f9f5eb23ae58ab'
         '50297577b500234b6c89fc89f909cb35')

prepare() {
  cd pynac-pynac-$pkgver
# Fix giac detection
  patch -p1 -i ../pynac-giac.patch
}

build() {
  cd pynac-pynac-$pkgver

  export PYTHON=python2
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd pynac-pynac-$pkgver
  make DESTDIR="$pkgdir" install
}
