# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>

pkgname=xmlrpc-c
# Using the "stable" version
pkgver=1.43.08
pkgrel=2
epoch=1
pkgdesc='XML-RPC for C and C++'
arch=('x86_64')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl')
makedepends=('libtool' 'setconf')
options=('!emptydirs')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tgz")
sha256sums=('c9f5b584a42493877ae0f09ed680d94e035ab389e8fa1873b1ec42118d5cfca3')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-libxml2-backend \
    --disable-cgi-server \
    --disable-libwww-client \
    --disable-wininet-client \
    --enable-cplusplus

  mkdir -p include/curl
  touch include/curl/types.h

  make CFLAGS_PERSONAL="$CFLAGS -fPIC -w" CXXFLAGS_PERSONAL="$CXXFLAGS -fPIC -w"
  make -C tools CFLAGS_PERSONAL="$CFLAGS -fPIC -w" CXXFLAGS_PERSONAL="$CXXFLAGS -fPIC -w"
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C tools install

  # Remove references to build directory
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" BLDDIR ''
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" ABS_SRCDIR ''

  # Custom license
  install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
